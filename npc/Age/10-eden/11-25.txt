//===== rAthena Script =======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,36,38,3	script	Mission [11 - 25]#Tuto	4_BOARD3,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Precisa de ter um -";
		mes "- ^4d4dff'Marca do Grupo Eden'^000000 -";
		mes "- para receber estas missões. -";
		close;
	}
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes " - Deve recolher os seus - ";
			mes " - recompensa antes de começar - ";
			mes " - uma nova missão. - ";
			mes " ";
			mes " - Gerente : Espigão - ";
			close;
		}
		else if (.@j >= 0) {
			mes " - Só pode solicitar - ";
			mes " - uma missão de cada vez. - ";
			mes " ";
			mes " - Gerente : Espigão - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - Não há tarefas - ";
		mes " - aberto para si agora mesmo. - ";
		close;
	}
	if (BaseLevel > 25) {
		mes " - Estas missões são também - ";
		mes " - fácil para o seu calibre - ";
		mes " - procura de um nível mais elevado - ";
		mes " - missão apropriada. - ";
		close;
	}
	mes " - Quadro de boletim da missão -";
	mes " - para principiantes. -";
	mes " ";
	mes " - Gerente : Espigão -";
	next;
	switch(select("Caça Zangão:Caça Condor:Caça Rockers:Caça Rabo de Verme:Caça Esporo:Caça Ovo de Besouro-Ladrão:Caça Muka:Caça Familiar:Caça Peco-Peco:Caça Esporo Venenoso:Cancela")) {
		case 1: callsub L_Quest,11114,"Caça Zangão"," - Caça 10 ^4d4dff'Zangão'^000000 - "," - Noroeste de Prontera. - ";
		case 2: callsub L_Quest,11115,"Caça Condor"," - Caça 20 ^4d4dff'Condors'^000000 - "," - Sul de Morocc. - ";
		case 3: callsub L_Quest,11116,"Caça Rockers"," - Caça 10 ^4d4dff'Rockers'^000000 - "," - sudoeste de Prontera e - "," - trazer 10 ^4d4dffPerna de Gafanhoto^000000. - ";
		case 4: callsub L_Quest,11117,"Caça Rabo de Verme"," - Caça 20 ^4d4dff'Rabo de Verme'^000000 - "," - sudoeste de Payon.- ";
		case 5: callsub L_Quest,11118,"Caça Esporo"," - Caça 30 ^4d4dff'Esporos'^000000 - "," - por volta de Payon. - ";
		case 6: callsub L_Quest,11119,"Ovo de Besouro-Ladrão"," - Caça 20 ^4d4dffOvo de Besouro-Ladrão^000000 - "," - no canteiro de Prontera - "," - e trazer 10 ^4d4dffChrysalis^000000. - ";
		case 7: callsub L_Quest,11120,"Caça Muka"," - Caça 20 ^4d4dff'Muka'^000000 - "," - Sul of Prontera. - ";
		case 8: callsub L_Quest,11121,"Caça Familiar"," - Caça 20 ^4d4dffFamiliar^000000 - "," - nos esgotos de Culvert - "," - oeste de Prontera. - ";
		case 9: callsub L_Quest,11122,"Caça Peco-Peco"," - Caça 30 ^4d4dff'Peco Pecos'^000000 - "," - Sul of Prontera. - ";
		case 10: callsub L_Quest,11123,"Caça Esporo Venenoso"," - Caça 30 ^4d4dffEsporo Venenoso^000000 - "," - dentro da Torre de Geffen e - "," - Trazer 5 ^4d4dffEsporos de Veneno^000000. - ";
		case 11:
			mes " - Parar de ler o quadro de boletins. - ";
			close;
	}

L_Quest:
	.@quest_status = checkquest(getarg(0)+10,PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes " - Missão ^4d4dff"+ getarg(1) +"^000000 - ";
		mes " - já está terminado para hoje. - ";
		mes " ";
		mes " - Gerente : Espigão - ";
		close;
	}
	mes " - Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	mes " ";
	mes " - Gerente : Espigão - ";
	next;
	if (select("Cumprirei esta missão.:Procurar outras missões.") == 2) {
		mes " - Vamos procurar por -";
		mes " - outras missões. - ";
		close;
	}
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Caça Zangão",900;
				case 11115: callsub L_Quest,.@i,"Caça Condor",1800;
				case 11116: callsub L_Quest,.@i,"Caça Rockers",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Caça Rabo de Verme",2600;
				case 11118: callsub L_Quest,.@i,"Caça Esporo",3900;
				case 11119: callsub L_Quest,.@i,"Ovo de Besouro-Ladrão",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Caça Muka",3200;
				case 11121: callsub L_Quest,.@i,"Caça Familiar",3200;
				case 11122: callsub L_Quest,.@i,"Caça Peco-Peco",5400;
				case 11123: callsub L_Quest,.@i,"Caça Esporo Venenoso",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes "[Spike]";
			mes "Ainda não terminou a sua missão.";
			mes "Continuar o bom trabalho. Está quase a terminar.";
			close;
		}
	}
	mes "[Spike]";
	mes "Está à procura de um emprego?";
	mes "Temos sempre muito trabalho para fazer.";
	next;
	mes "[Spike]";
	mes "Porque não olha à sua volta e encontra algumas tarefas interessantes?";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes "[Spike]";
				mes "Não tem o suficiente "+ getitemname(.@item_req) +".";
				mes "Tem de trazer "+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes "[Spike]";
		mes "Hmm... deixe-me ver~";
		mes "Concluiu o '"+ getarg(1) +"' Missão.";
		next;
		mes "[Spike]";
		mes "Bom trabalho~!!";
		mes "O nosso cliente está muito satisfeito com o seu trabalho. Ele irá apreciar o que fez.";
		mes "Por favor, ajude-nos novamente. Obrigado!";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		close;
	}
}
