//===== rAthena Script =======================================
//= Eden Group Quests - Mission [56 - 70]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 56 - 70.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Added checkquest confirmation for -1. [Euphy]
//= 1.3 Fixed Puppet Master's Agony Quest (12217 -> 3259). [Joseph]
//= 1.4 Fixed invalid check (Magic Wand Quest). [Joseph]
//= 1.5 Optimized, based on code by Zopokx. [Euphy]
//= 1.6 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.7 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,42,38,3	script	Mission [56 - 70]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Para conseguir estas missões, preciso de me juntar primeiro ao Grupo do Éden. Tenho de encontrar o Secretário Lime Evenor e tornar-me membro.";
		close;
	}
	mes "Aqui está a lista de várias aventuras para aventureiros de nível 56~70.";
	next;
	mes "Muitas missões tais como a caça ao monstro, entrega de bens, procura de pessoas.";
	next;
	switch(select("Números da Missão 1~5:Números da Missão 6~10:Números da Missão 11~15")) {
	case 1:
		switch(select("O que é este curativo?:Jacarés Perigosos.:Não era uma sereia...:O meu país chama-me!")) {
			case 1: callsub L_HuntingQuest,3250,15000,5000,930,30;
			case 2: callsub L_HuntingQuest,3251,16000,6000;
			case 3: callsub L_HuntingQuest,3252,17000,8000;
			case 4: callsub L_Quest,3254,18000,8000,7043,10,1056,30;
		}
	case 2:
		switch(select("Hunt Wild Boar:Preparando o Verão:O rancor de uma mulher:Ingredientes especiais:Agonia do Mestre das Marionetas")) {
			case 1: callsub L_HuntingQuest,3255,16000,7000;
			case 2: callsub L_Quest,3256,15000,5000,918,30;
			case 3: callsub L_HuntingQuest,3257,17000,8000;
			case 4: callsub L_Quest,3258,16000,7000,7198,40;
			case 5: callsub L_HuntingQuest,3259,17000,7000,1060,30;
		}
	case 3:
		switch(select("Tansome Flies:Dangerous Munak:Make the World green:Varinha de condão mágica")) {
			case 1: callsub L_HuntingQuest,3260,16000,6000;
			case 2: callsub L_HuntingQuest,3261,16000,7000;
			case 3: callsub L_Quest,3262,17000,7000,1032,40;
			case 4: callsub L_Quest,3263,18000,8000,1021,30,7150,30;
		}
	}
	end;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "Deseja aceitar esta missão?";
		next;
		if(select("Aceitar a missão.:Não aceitar a missão.") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "Estas missões não são adequadas para o meu nível. Devo procurar outras missões.";
				close;
			}
			setquest .@quest_id;
			mes "Aceitei com sucesso a missão. Tenho de voltar e confirmar o meu trabalho depois de completar a missão..";
			next;
			mes "Esta missão não tem limites de tempo.";
		}
		close;
	}
	if (getargcount() > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id) < 2 && (countitem(getarg(3)) < getarg(4) || .@items == 1)) {
		mes "Tem uma missão em curso. Deseja verificar os detalhes?";
		next;
		if(select("Verifique os detalhes.:Cancelar.") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else {
		mes "Tenho-me saído muito bem para a missão. Se o relatar agora?";
		next;
		if(select("Relatar a missão.:Não o comunique ainda.") == 1) {
			delitem getarg(3),getarg(4);
			if (.@items)
				delitem getarg(5),getarg(6);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "Concluiu a missão. Receba recompensas.";
		}
		close;
	}
	end;

L_HuntingQuest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "Deseja aceitar esta missão?";
		next;
		if(select("Aceitar a missão.:Não aceitar a missão.") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "Estas missões não são adequadas para o meu nível. Devo procurar outras missões.";
				close;
			}
			setquest .@quest_id;
			mes "Aceitei com sucesso a missão. Tenho de voltar e confirmar o meu trabalho depois de completar a missão..";
			next;
			mes "Esta missão não tem limites de tempo.";
		}
		close;
	}
	if (getargcount() > 3) {
		if (countitem(getarg(3)) < getarg(4))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id,HUNTING) < 2 || .@items == 1) {
		mes "Tem uma missão em curso. Deseja verificar os detalhes?";
		next;
		if(select("Verifique os detalhes.:Cancelar.") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else if (checkquest(.@quest_id,HUNTING) == 2) {
		mes "Tenho-me saído muito bem para a missão. Devo denunciá-lo agora?";
		next;
		if(select("Relatar a missão.:Não o comunique ainda.") == 1) {
			if (.@items)
				delitem getarg(3),getarg(4);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "Concluiu a missão. Receba recompensas.";
		}
		close;
	}
	end;

L_Details:
	switch(getarg(0)) {
	case 3250:
		mes "Esta missão é atribuída pela corporação Rekenber de Lighthalzen. Abaixo estão os detalhes";
		next;
		mes "A Rekenber Corporation está prestes a lançar um novo projecto chamado 'Green Medical Center'. Para o primeiro produto do projecto, estamos a preparar ligaduras de bem-estar";
		next;
		mes "Em relação ao projecto, precisamos de investigar as reacções dos monstros quando estes são embrulhados com ligaduras podres";
		next;
		mes "Se estiver interessado, por favor caçar 30 múmias e trazer 30 ligaduras podres. Pode encontrá-las na Pirâmide Oeste de Marrocos";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos as devidas recompensas";
		next;
		mes "- directora chefe da corporação Rekenber 'Julie E Delph' -";
		break;
	case 3251:
		mes "Esta missão é atribuída pela sociedade cooperativa Comodo. Abaixo estão os detalhes";
		next;
		mes "Os jacarés enlouqueceram hoje em dia, atacam mulheres e pessoas bêbadas que andam por perto da praia";
		next;
		mes "Esta é uma situação muito vergonhosa para nós, o mais maravilhoso local de férias em Rune-Midgarts";
		next;
		mes "Portanto, seria muito útil se caçassem 30 crocodilos para limpar esta situação";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, então dar-lhe-emos recompensas adequadas";
		break;
	case 3252:
		mes "Esta missão é atribuída por um cliente desconhecido da Izlude";
		next;
		mes "Admiro a praia de Izlude, desde que nasci";
		next;
		mes "Quando era jovem, encontrei uma criatura misteriosa no calabouço de Izlude, nunca tinha visto algo assim antes...";
		next;
		mes "Pensei imediatamente que era uma sereia. Fiquei tão supreendido, que estudei sobre sereias durante toda a minha vida";
		next;
		mes "Mas, enquanto estudava mais, descobri que a criatura não era uma sereia... sim, não era nada bela como as outras sereias...";
		next;
		mes "Finalmente descobri o facto de que se tratava de um Homem, não de uma Sereia!";
		next;
		mes "Merman... Desperdicei a minha vida a estudar este monstro! Por isso, por favor caçam 30 Alemães para me fazerem sentir melhor";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "-- Izlude 'A sereia dos sonhos perdidos' --";
		break;
	case 3254:
        mes "Esta missão é atribuída pelo oficial do governo do Reino de Rune-Midgarts";
		next;
		mes "Para evitar uma inundação em Prontera, decidimos construir uma enorme barragem para regular o abastecimento de água";
		next;
		mes "Mas, estamos com falta de água e precisamos de recolher Areia Fina e Grão";
		next;
		mes "Por isso, gostaríamos de pedir ajuda. Pessoas, por favor tragam-nos 10 Areias Finas e 30 Grãos cada uma, para que possamos construir a barragem";
		next;
		mes "Podem obter esses materiais do Sand Man, desejo-vos boa sorte!";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "-- Oficial do Governo do Reino de Rune-Midgarts, Dufre Kent --";
		break;
	case 3255:
		mes "Esta missão é atribuída pela Câmara Municipal de Payon";
		next;
		mes "Os animais selvagens são sempre aborrecidos para os agricultores";
		next;
		mes "Eles atacam a nossa quinta e arruínam colheitas inteiras!! Já não aguentamos mais!";
		next;
		mes "Portanto, gostaríamos de pedir a ajuda de corajosos aventureiros";
		next;
		mes "Por favor, caçam 30 selvagens para salvar as nossas colheitas!";
		next;
		mes "Pode facilmente encontrar esses monstros à volta da nossa cidade e do nosso campo";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "-- Membros da câmara municipal de Payon. --";
		break;
	case 3256:
		mes "Esta missão é atribuída pelo ferreiro marroquino Aragham";
		next;
		mes "Olá~~~~, Aventureiros! Este é Aragham, o ferreiro mais quente de Marrocos!";
		next;
		mes "De qualquer modo, tenho um filho chamado Aragam Junior, o bonitinho. Haha.";
		next;
		mes "Ele vai estar no acampamento de verão este verão, mas não sabe nadar";
		next;
		mes "Eu quero ensiná-lo, mas como sabem eu sou ferreiro, um chorão longe de nadar! Hehe";
		next;
		mes "Mas, pensei que se eu fizer barbatanas de natação, será óptimo para a sua confiança~!";
		next;
		mes "Para o fazer, preciso de 30 pés de teia pegajosos de um Sapo Roda. Podes trazê-los até mim?";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "PS. Não me comparem com Hollgrehenn ou Antonio!!!!";
		next;
		mes "-- O ferreiro mais quente, o ferreiro marroquino Aragham --";
		break;
	case 3257:
		mes "Esta missão é atribuída por um inventor Dorian de Izlude";
		next;
		mes "Já ouviram a história de que se as mulheres têm um rancor na cabeça, isso trará desastres naturais";
		next;
		mes "Uau, por isso os homens devem ter cuidado com essas mulheres sensíveis! Certifica-te de que elas não têm ressentimentos contra ti";
		next;
		mes "As mulheres podem trazer fenómenos estranhos com elas!";
		next;
		mes "Estou tão intrigado com essa história que estou a tentar provar que ela pode ser verdadeira";
		next;
		mes "Por isso, preciso de perturbar as mulheres! Haha, eu sei o que é uma ideia má, certo?";
		next;
		mes "Mas isto é a sério só para estudar... Por isso, por favor caçam 30 Sohee's que parecem ter ressuscitado das almas vitimizadas";
		next;
		mes "Para que eu possa continuar a observar o caso";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos as devidas recompensas";
		next;
		mes "-- Elegance inventor Dorian --";
		break;
	case 3258:
		mes "Esta missão é atribuída por Wallah de Payon";
		next;
		mes "Olá, aventureiro. Como está? Como está a sua saúde?";
		next;
		mes "Sou farmacêutico e ultimamente estou a ter dificuldades em obter ingredientes especiais";
		next;
		mes "Porque sou uma menina sensível, como é que posso conseguir essas coisas sozinha";
		next;
		mes "Portanto, por favor, ajudem-me. Basta trazer 40 Huge Leafs para mim, isso será suficiente para completar o meu remédio especial";
		next;
		mes "Por favor, traga-o o mais depressa possível, as pessoas precisam do meu remédio";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos as devidas recompensas";
		next;
		mes "-- Wallah --";
		break;
	case 3259:
		mes "Esta missão é atribuída pelo mestre fantoche Woonute de Geffen";
		next;
		mes "Sabe que ser um mestre fantoche é uma tarefa difícil para satisfazer as pessoas";
		next;
		mes "As pessoas podem procurar e ver tantas coisas diferentes nos seus próprios dias";
		next;
		mes "As suas expectativas estão a ficar altas, já não as consigo satisfazer";
		next;
		mes "Mas se eu conseguir fazer novos conceitos de brinquedo, como brinquedos vivos, será uma ruptura de terra";
		next;
		mes "Ouvi dizer que há bonecas vivas chamadas Marionettes. Podes caçar 30 Marionettes para mim? E também trazer 30 Golden Hair, esses serão óptimos materiais para os novos brinquedos";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "-- Puppet Master Woonute --";
		break;
	case 3260:
		mes "Esta missão é atribuída por um exterminador de Prontera";
		next;
		mes "À medida que o tempo vai aquecendo, os insectos multiplicam-se cada vez mais. Já está fora de controlo";
		next;
		mes "As pessoas em Prontera não conseguem dormir por causa de todos os insectos na sua casa";
		next;
		mes "Então, por favor, ajudem-nos, caçam 30 moscas caçadoras. Então, as moscas podem ser reduzidas lentamente";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos as devidas recompensas";
		next;
		mes "-- O exterminador de Prontera --";
		break;
	case 3261:
		mes "Esta missão é atribuída pelo Cheese Dongja de Payon";
		next;
		mes "Já ouviu falar de Munak?";
		next;
		mes "O monstro feio Munak está a ameaçar as pessoas em Payon";
		next;
		mes "Então precisamos de aventureiros corajosos como você!";
		next;
		mes "Por favor caçar 30 Munaks, então Payon estará em paz";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "- Payon Cheese Dongja -";
		break;
	case 3262:
		mes "Esta missão é atribuída por um negociante de pedras preciosas, Ibraham de Marrocos";
		next;
		mes "Uma pequena e bela flor no deserto estéril... Que cena incrível seria!";
		next;
		mes "Plantar flores pode tornar o mundo verde e será tão bonito em todo o lado";
		next;
		mes "Plantar flores fortes no deserto marroquino para que o solo melhore";
		next;
		mes "Preciso de 40 Maneater Blossom da Flora, sei que Maneater Blossom é uma flor feia, mas mesmo assim é uma planta";
		next;
		mes "Espero que possas caçar Flora e arranjar-me 40 Flores de Feiticeiro. Estás a tornar o mundo verde! Sê orgulhoso!";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, depois dar-lhe-emos recompensas adequadas";
		next;
		mes "-- Morocc Ibraham --";
		break;
	case 3263:
		mes "Esta missão é atribuída pelo ferreiro Payon Antonio";
		next;
		mes "Varinha de condão mágica! Já ouviu falar disso? É uma varinha de condão incrível";
		next;
		mes "Se disseres as palavras mágicas e balançares a varinha, ela fará com que os teus desejos se tornem realidade";
		next;
		mes "Então, decidi fazê-la sozinho. Então serei rico e poderoso~! Haha!!";
		next;
		mes "Para fazer a varinha, preciso de alguns materiais especiais";
		next;
		mes "Por favor traga-me 30 chifres de Dokebi e 30 cortes de bambu. Se o fizerem, empresto-vos a minha varinha de condão uma vez. Haha, bom negócio?";
		next;
		mes "Por favor, confirme-nos depois de completar a missão, então dar-lhe-emos recompensas adequadas";
		next;
		mes "-- Payon blacksmith Antonio --";
		break;
	}
	return;
}
