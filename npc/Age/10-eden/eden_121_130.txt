//===== rAthena Script =======================================
//= Eden Group Quests - Mission [121-130]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 121 - 130.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,81,3	script	Melody-Jack#1	4_M_MELODY,8,8,{
	mes "[Melody-Jack]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Quem diabos e voce? Saiam daqui!";
		mes "So estou a falar com aventureiros de nivel base ^FF0000121-130^000000";
	}
	else if (para_120lv01 == 0)
		mes "O que? nao me incomodem. Estou ocupado agora";
	else if (para_120lv01 == 1) {
		mes "Voce recebeu um pedido daquela garotinha? A pequena, faladora e semi-nua ali?";
		next;
		select("Diga que sim", "Evite responder..");
		mes "[Melody-Jack]";
		mes "Nao. Nao me importo se voce entrou nesta sala, nao porque esta perdido. Eu preciso de qualquer um, exceto esse tipo de tolo";
		next;
		mes "[Melody-Jack]";
		mes "Por favor, escutem-me, eu lhes oferecerei uma sugestao atraente";
		next;
		mes "[Melody-Jack]";
		mes "Eu sou ^ff0000Melody - Jack!^000000 O melhor homem com a melhor adaga";
		next;
		mes "Melody-Jack desliza uma adaga em sua direcao";
		next;
		mes "[Melody-Jack]";
		mes "Estou qualificado para assumir missoes secretas. E neste momento, voce esta com sorte";
		next;
		mes "[Melody-Jack]";
		mes "Parece que tenho que perder uma missao, negociar e prometer, nao posso aceita-la agora mesmo";
		next;
		mes "[Melody-Jack]";
		mes "Aceite minha missao e resolva o pedido da Aigu. Eu o compensarei";
		next;
		mes "[Melody-Jack]";
		mes "Nosso acordo sera mantido como um segredo para a Aigu. Se voce nao tiver 9 vidas. Vamos comecar";
		para_120lv01 = 2;
	}
	else if (para_120lv01 > 1) {
		mes "......";
		next;
		if (select( "Mostre-me os pedidos", "Faca pequenas conversas" ) == 2) {
			mes "[Melody-Jack]";
			mes "O que? Voce esta interessado em mim??";
			next;
			switch( select( "Estou interessado", "Sobre pedidos", "Sobre um acordo", "O que e uma promessa?" ) ) {
				mes "[Melody-Jack]";
			case 1:
				mes "Bem, va embora. Eu nao tenho interesse em voce";
				close;
			case 2:
				mes "Nao posso impedi-lo por causa do meu credito ruim. Isso me deixa louco";
				next;
				mes "[Melody-Jack]";
				mes "Tenho a sorte de fazer um duplo contrato com um aventureiro como voce. Eles nao estao incluidos em nosso contrato";
				close;
			case 3:
				mes "Para ser honesto, acho que nao ha ninguem digno de confianca. Confianca ou promessas sao ambas bobas";
				next;
				mes "[Melody-Jack]";
				mes "Confio somente em mim e no meu acordo. A confianca ou promessas sao vazias se forem quebradas, mas se um acordo for quebrado, ha compensacao ou vinganca";
				close;
			case 4:
				mes "Quem sao todas essas criancas?";
				next;
				mes "[Melody-Jack]";
				mes "Eu sai por nada, mas devo cumprir meu dever. Eu nao devo nada a ninguem";
				close;
			}
		}
		mes "[Melody-Jack]";
		if (BaseLevel < 121)
			mes "Oh, eu cometi um erro. Voce nao e o nivel de aventureiro que eu procuro. Procuro aventureiros de nivel basico 121 a 130";
		else if (BaseLevel < 131) {
			mes "Nao gosto disso, mas um acordo e um acordo. Por favor, volte depois de limpar os monstros nos lugares onde Aigu pediu para procurar";
			next;
			switch( select( "Thanatos Tower related", "Rachel Shrine related", "Nameless Island related", "Abyss Lake related." ) ) {
				mes "[Melody-Jack]";
			case 1:
				mes "A torre de Thanatos e o lugar mais irritante. Nao se preocupe com sua compensacao. Eu cumpro minha palavra";
				next;
				switch( select( "Baronesa da Retribuicao", "Senhora Solace", "Senhora do Abrigo", "Dama de Sentinela." ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13120,PLAYTIME) == 0)
						mes "Acho que terminei a apresentacao de hoje. Eu nao posso relaxar e se os monstros aparecerem de repente.";
					else if (checkquest(13119,HUNTING) == 1) {
						cutin "신벌의대행자카드",3;  //retribution_card
						mes "Nao gosto dessa criatura nao porque a Aigu nao gosta deles... Como voce ousa? Quem esta castigando quem?";
						next;
						mes "[Melody-Jack]";
						mes "Estas sao algumas das criaturas mais perigosas do mundo. Estas criaturas nao merecem justica";
						next;
						cutin "",255;
						mes "[Melody-Jack]";
						mes "E claro que voce pode confiar em mim. Porque eu acredito em negocios reais. Vamos varrer as criaturas";
					}
					else if (checkquest(13119,HUNTING) == 2) {
						mes "Voce os liberou? Entao isso nao incomoda aquele garotinho por um dia. Bom trabalho";
						callsub S_Quest,13119,13120,1;
					}
					else {
						cutin "신벌의대행자카드",3;  //retribution_card
						mes "Como voce sabe, a Torre de Thanatos e onde Aigu mais se importa. E o lugar mais perigoso em alguns aspectos";
						next;
						mes "[Melody-Jack]";
						mes "Por favor, limpe os monstros para ajudar a Aigu. Os amadores vao fazer as coisas confusas";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare." ) == 1) {
							mes "[Melody-Jack]";
							mes "Deve haver muitos deles. Nao se esqueca de manter isto em segredo";
							callsub S_Quest,13120,13119;
						}
					}
					close;
				case 2:
					if (checkquest(13122,PLAYTIME) == 0)
						mes "Acho que terminei a apresentacao de hoje. Eu nao posso relaxar... E se os monstros aparecerem de repente?";
					else if (checkquest(13121,HUNTING) == 1) {
						cutin "위로하는자",3;  //solace_card
						mes "O edificio nao parece ter sido projetado por seres humanos e uma estranha melodia esta sempre tocando la";
						next;
						mes "[Melody-Jack]";
						mes "Nao caia no clima da melodia porque essa pode ser sua ultima cancao que voce ouve";
						next;
						cutin "",255;
						mes "[Melody-Jack]";
						mes "Lembre-se, ha musica estranha tao afiada como uma faca para matar pessoas no mundo";
					}
					else if (checkquest(13121,HUNTING) == 2) {
						mes "Voce ja ouviu a melodia que eles estao tocando? Voce tera uma chance se aquele garotinho comecar a tocar a performance novamente";
						callsub S_Quest,13121,13122,1;
					}
					else {
						cutin "위로하는자",3;  //solace_card
						mes "Bela melodia. Talvez soe linda porque as criaturas estao tocando";
						next;
						mes "[Melody-Jack]";
						mes "No entanto, esse e outro obstaculo. Um acordo e anterior ao interesse pessoal a qualquer momento. Tao claro, Lady Solace";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Estamos apenas cumprindo a nossa parte do acordo. Limpem essas criaturas agora";
							callsub S_Quest,13122,13121;
						}
					}
					close;
				case 3:
					if (checkquest(13124,PLAYTIME) == 0)
						mes "Acho que terminei a apresentacao de hoje. Eu nao posso relaxar... E se os monstros aparecerem de repente?";
					else if (checkquest(13123,HUNTING) == 1) {
						cutin "안식을주는자카드",3;  //shelter_card
						mes "Voce conhece um anjo que se transformou em um monstro? Eu nao sei se foi um anjo ou nao. Mas sua aparencia era um anjo";
						next;
						mes "[Melody-Jack]";
						mes "Nao entre em panico devido as belas aparencias, independentemente de ser um anjo ou uma criatura. Voce deve fazer seu trabalho";
						next;
						mes "[Melody-Jack]";
						mes "Tudo tem seu tempo. Talvez voce tenha solicitado isto porque esse tempo chegou";
						cutin "",255;
					}
					else if (checkquest(13123,HUNTING) == 2) {
						mes "Bom. Avise-me se alguem estiver procurando por mim. Se for o caso, talvez eu precise mudar os detalhes do acordo";
						callsub S_Quest,13123,13124,1;
					}
					else {
						cutin "안식을주는자카드",3;  //shelter_card
						mes "Voce conhece um anjo que se transformou em um monstro? Eu nao sei se foi um anjo ou nao. Mas sua aparencia era um anjo";
						next;
						mes "[Melody-Jack]";
						mes "Nao me importa se e um anjo de verdade ou apenas uma criatura. Clear Mistress of Shelter";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Flores moribundas caem pela chuva e pelo vento. Isso e o que voce tem que fazer";
							emotion ET_BEST;
							callsub S_Quest,13124,13123;
						}
					}
					close;
				case 4:
					if (checkquest(13126,PLAYTIME) == 0)
						mes "Terminei meu desempenho por hoje. Nao posso relaxar e se os monstros aparecerem de repente";
					else if (checkquest(13125,HUNTING) == 1) {
						cutin "감시하는자카드",3;  //observation_card
						mes "Voce pode ter sentido que eles estao observando quando voce pisou na area superior da Torre de Thanatos";
						next;
						mes "[Melody-Jack]";
						mes "Dame of Sentinel nao e facil de manejar, portanto, esteja certo de estar pronto para lutar";
						next;
						mes "[Melody-Jack]";
						mes "E um belo dia para morrer. Sera voce ou eles que farao o seu melhor";
						cutin "",255;
					}
					else if (checkquest(13125,HUNTING) == 2) {
						mes "Voce parece bem. Talvez voce esteja melhor do que eu pensava? Mas eu nao verifiquei com meus olhos";
						callsub S_Quest,13125,13126,1;
					}
					else {
						cutin "감시하는자카드",3;  //observation_card
						mes "Voce ira encontra-los se a Aigu solicitar. A Dama de Sentinelas";
						next;
						mes "[Melody-Jack]";
						mes "Desejo que eles tambem sejam liberados para que ela solicite esta aventura facilmente";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare." ) == 1) {
							mes "[Melody-Jack]";
							mes "Um pedido de um pedido... Tanto faz";
							callsub S_Quest,13126,13125;
						}
					}
					close;
				}
			case 2:
				mes "A aventura de Aigu leva ao Santuario de Raquel. Certo... Nao e estranho se ^FF0000 que o ^ humano ^000000 Aigu procurando e encontrado em qualquer lugar";
				next;
				switch( select( "Vanberk and Isilla", "Hodremlin", "Agav and Echio" ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13128,PLAYTIME) == 0)
						mes "Voce pode descansar por um dia. Voce se liberta o suficiente, mas o fanatico e aumentado";
					else if (checkquest(13127,HUNTING) == 1) {
						cutin "이실라카드",3;  //isilla_card
						mes "Alguem que acredita estar certo e o mais perigoso. Este mundo esta cheio de tolos";
						next;
						mes "[Melody-Jack]";
						mes "Finalmente forcar outros a acreditar no que eles acreditam. Deixemos claro Vanberk e Isilla";
						next;
						mes "[Melody-Jack]";
						mes "Nao tenho nada a ver com isso. Mas desta vez e diferente. Um garotinho pode estar em perigo, entao vamos nos apressar para limpar estes monstros";
						cutin "",255;
					}
					else if (checkquest(13127,HUNTING) == 2) {
						mes "Just a day. Will be fine.";
						callsub S_Quest,13127,13128,1;
					}
					else {
						cutin "판베르크카드",3;  //vanberk_card
						mes "Santuario em Raquel e proibido as pessoas normais de entrar. Voce tem que saber disso";
						next;
						mes "[Melody-Jack]";
						mes "Eles acham que podem encontrar ali uma pista, mas nao e um lugar seguro. Voce deve limpar Vanberk e Isilla com antecedencia";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare" ) == 1) {
							mes "[Melody-Jack]";
							mes "Limpe tudo o mais rapido que puder.";
							emotion ET_SPARK;
							callsub S_Quest,13128,13127;
						}
					}
					close;
				case 2:
					if (checkquest(13130,PLAYTIME) == 0)
						mes "Os mosteiros desconhecidos sao perigosos, por isso os limpe primeiro. Eu os informarei se algo acontecer";
					else if (checkquest(13129,HUNTING) == 1) {
						cutin "호드렘린카드",3;  //hodremlin_card
						mes "Os dentes frios podem estar se aproximando. Seus dentes sao mais rapidos que suas maos";
						next;
						mes "[Melody-Jack]";
						mes "O santuario de Raquel se aproximara de todos os lugares. Voce deve ter cuidado com este aqui";
						next;
						mes "[Melody-Jack]";
						mes "Basta pensar no trabalho em questao. Isso sera suficiente";
						cutin "",255;
					}
					else if (checkquest(13129,HUNTING) == 2) {
						mes "Eu nao estou tao preocupado, mas voce esta seguro? Nao mordido?";
						callsub S_Quest,13129,13130,1;
					}
					else {
						cutin "호드렘린카드",3;  //hodremlin_card
						mes "Os fanaticos sao um problema. Um problema mais serio e que eles aparecem de repente";
						next;
						mes "[Melody-Jack]";
						mes "Livre-se de ^FF0000Hodremlin^000000 sem qualquer hesitacao. Porque eles sao muito perigosos";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare" ) == 1) {
							mes "[Melody-Jack]";
							mes "Eles precisam de maos mais rapidas do que uma cabeca. Eu nao me preocupo, mas voce deve ter cuidado";
							callsub S_Quest,13130,13129;
						}
					}
					close;
				case 3:
					if (checkquest(13132,PLAYTIME) == 0)
						mes "Se voce puder falar. Nao ha solicitacoes neste momento. Descanse um pouco por hoje";
					else if (checkquest(13131,HUNTING) == 1) {
						cutin "에키오카드",3;  //echio_card
						mes "Se voce puder falar. Nao ha solicitacoes neste momento. Descanse um pouco por hoje";
						next;
						mes "[Melody-Jack]";
						mes "E a minha sensacao, mas voce pode gostar. Tenho certeza que ^FF0000Agav^000000 e ^FF0000Echio^000000 nao vao gostar de voce";
						next;
						mes "[Melody-Jack]";
						mes "Eles se encontrarao com voce. Nao hesite e mantenha-se concentrado no trabalho";
						cutin "",255;
					}
					else if (checkquest(13131,HUNTING) == 2) {
						mes "Encontrou alguem? Eu nao me preocuparia. Uma promessa ia uma promessa e um acordo e um acordo";
						callsub S_Quest,13131,13132,1;
					}
					else {
						cutin "아가브카드",3;  //agav_card
						mes "Encontrou alguem? Eu nao me preocuparia. Uma promessa ia uma promessa e um acordo e um acordo";
						next;
						mes "[Melody-Jack]";
						mes "Isso nao foi tao ruim assim. Mas alguem precisa ajudar? Vamos limpar ^FF0000Agav^000000 e ^FF0000Echio^000000 dentro do Santuario de Raquel";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare." ) == 1) {
							mes "[Melody-Jack]";
							mes "Nao posso partir tao longe.";
							callsub S_Quest,13132,13131;
						}
					}
					close;
				}
			case 3:
				mes "Ilha sem nome nao e conhecida pelos aventureiros habituais. Aigu tambem se interessa por la";
				next;
				switch( select( "Caca ao Zumbi Desorientado", "Caca ao Zumbi Abatido", "Caca ao Banshee." ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13134,PLAYTIME) == 0)
						mes "O zumbi deformado e reduzido. E um bom momento para proceder o pedido da Aigu por um minuto";
					else if (checkquest(13133,HUNTING) == 1) {
						cutin "누더기좀비카드",3;  //ragged_zombie_card
						mes "E facil encontrar tudo na Ilha Sem Nome. Tao entediante.";
						next;
						mes "[Melody-Jack]";
						mes "Livre-se dele e ajude Aigu e os outros aventureiros";
						cutin "",255;
					}
					else if (checkquest(13133,HUNTING) == 2) {
						mes "Voce tomou um banho? Cheira aqui dentro. Mas tenho certeza de que voce passou um mau bocado com o cheiro";
						callsub S_Quest,13133,13134,1;
					}
					else {
						cutin "누더기좀비카드",3;  //ragged_zombie_card
						mes "Os mortos voltam a noite. Voltar soa estranho. Vamos parar com essa porcaria";
						next;
						mes "[Melody-Jack]";
						mes "Os mortos voltam a noite. Voltar soa estranho. Vamos parar com essa porcaria";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare" ) == 1) {
							mes "[Melody-Jack]";
							mes "Tome um ^FF0000bath^000000 e voce nao sentira mais o cheiro";
							callsub S_Quest,13134,13133;
						}
					}
					close;
				case 2:
					if (checkquest(13136,PLAYTIME) == 0)
						mes "Eu sei que o pedido para a Ilha Sem Nome nao e facil, mas ficarei feliz se voce se juntar novamente";
					else if (checkquest(13135,HUNTING) == 1) {
						cutin "좀비슬러터카드",3;  //zombie_slaughter_card
						mes "Tambem nao foi facil para mim entrar na Ilha Sem Nome. Eu nao sou tao paciente assim";
						next;
						mes "[Melody-Jack]";
						mes "Mas os aventureiros devem desafiar uma vez. ^FF0000Zombie Slaughter^000000 vale a pena limpar";
						cutin "",255;
					}
					else if (checkquest(13135,HUNTING) == 2) {
						mes "Voce e muito bom! Voce os liberou. Vai levar tempo para voltar";
						callsub S_Quest,13135,13136,1;
					}
					else {
						cutin "좀비슬러터카드",3;  //zombie_slaughter_card
						mes "Eles sao perigosos, ao contrario do Zumbi Ardiloso, porque balancam algo afiado";
						next;
						mes "[Melody-Jack]";
						mes "Por favor, limpe ^FF0000Zombie Slaughter^000000. Tenha cuidado para evitar suas laminas";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare" ) == 1) {
							mes "[Melody-Jack]";
							mes "Voce tem muitas coisas para cuidar, por isso vou segurar minhas palavras. Esvazie-as";
							callsub S_Quest,13136,13135;
						}
					}
					close;
				case 3:
					if (checkquest(13138,PLAYTIME) == 0)
						mes "Banshee esta livre. O pedido e resolvido de qualquer forma, mas nao e o fim. Mais cedo ou mais tarde, sera necessario novamente";
					else if (checkquest(13137,HUNTING) == 1) {
						cutin "밴시카드",3;  //banshee_card
						mes "Voce vera que ha coisas indesejaveis enquanto se aventuram as vezes independentemente de monstros e companheiros";
						next;
						mes "[Melody-Jack]";
						mes "Este pedido e assim para mim. Eu simplesmente nao gosto de criaturas ou da razao de dar este pedido";
						cutin "",255;
					}
					else if (checkquest(13137,HUNTING) == 2) {
						mes "Voce deve estar cansado. Voce foi para aquele lugar dificil, alem de sua capacidade. Nao fique bravo. Eu vou recompensa-lo por isso";
						callsub S_Quest,13137,13138,1;
					}
					else {
						cutin "밴시카드",3;  //banshee_card
						mes "Por favor, limpe ^FF0000Banshee^000000 dentro da Ilha Sem Nome. Nao diga nao. Voce sabe que e para Aigu tambem";
						next;
						mes "[Melody-Jack]";
						mes "Tanto Banshee quanto Aigu sao irritantes. Eu nao faria este pedido, mas voce sera diferente. Certo?";
						next;
						cutin "",255;
						if (select( "Aceite o pedido", "Pare" ) == 1) {
							mes "[Melody-Jack]";
							mes "Ve? Alguem precisa deste pedido. Clear ^FF0000Banshees^000000 at Nameless Island";
							callsub S_Quest,13138,13137;
						}
					}
					close;
				}
			case 4:
				mes "Abyss Lake tem pedidos de uma garotinha. Muito dilligent";
				next;
				if (select( "Ferus and Bewler are clean", "Gold and Blue Acidus are clean" ) == 1) {
					mes "[Melody-Jack]";
					if (checkquest(13140,PLAYTIME) == 0)
						mes "Os dragoes verdes e os dragoes vermelhos me lembram as melancias.";
					else if (checkquest(13139,HUNTING) == 1) {
						cutin "레드페러스카드",3;  //ferus_card
						mes "O Lago Abismo... Voce possivelmente sabe como entrar la? Voce nao e um aventureiro com essa informacao?";
						next;
						mes "[Melody-Jack]";
						mes "So para o caso de eu lhe dar uma pista de que uma chave e uma entrada vao ser encontradas por la";
						cutin "",255;
					}
					else if (checkquest(13139,HUNTING) == 2) {
						mes "Voce limpou todos os dragoes? Descanse por um dia";
						callsub S_Quest,13139,13140,1;
					}
					else {
						cutin "그린페러스카드",3;  //ferus__card
						mes "O Lago Abismo esta cheio de dragoes que entram e saem. Os dragoes nao sao tao especiais assim, mas sao tantos";
						next;
						mes "[Melody-Jack]";
						mes "Qual deles e mais perigoso entre Ferus e Bewler? Eu nao sei. Limpe-os aos dois";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Nem o verde nem o vermelho sao perturbadores. Mas, independentemente disso, volte depois de limpar tudo";
							callsub S_Quest,13140,13139;
						}
					}
				}
				else {
					mes "[Melody-Jack]";
					if (checkquest(13142,PLAYTIME) == 0)
						mes "Descanse um pouco. Devemos ir e subjugar novamente os dragoes Acido.";
					else if (checkquest(13141,HUNTING) == 1) {
						cutin "블루어시더스카드",3;  //acidus__card
						mes "O Abismo do Lago nao esta proximo. Mas vale a pena visitar.";
						next;
						mes "[Melody-Jack]";
						mes "Isto e tecnicamente um exercicio de treinamento, mas apenas finja que voce esta ajudando alguem. Havera uma recompensa";
						cutin "",255;
					}
					else if (checkquest(13141,HUNTING) == 2) {
						mes "Voce e mais um aventureiro talentoso do que eu pensava. Se voce encontrou uma tatica, cuide dela da proxima vez. Este estudo e com voce";
						callsub S_Quest,13141,13142,1;
					}
					else {
						cutin "골드어시더스",3;  //acidus_card
						mes "Ha uma caverna onde os dragoes estao vivendo dentro do Lago Abismo. Ha tantas coisas, mas voar e irritante";
						next;
						mes "[Melody-Jack]";
						mes "Subjugue-os o melhor que puder. Nao se importe com as cores deles, pois todos eles devem ser limpos";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Subjugar os monstros do Ouro e do Acido Azul no ultimo nivel do Lago do Abismo";
							callsub S_Quest,13142,13141;
						}
					}
				}
				close;
			}
		}
		else {
			mes "Voce terminou de ajudar a Aigu ou voce desistiu? De qualquer forma, nao tenho mais negocios para voce.";
			next;
			callsub S_Erasequest,
				13119,"Baroness of Retribution",
				13121,"Lady Solace",
				13123,"Mistress of Shelter",
				13125,"Dame of Sentinel",
				13127,"Vanberk and Isilla",
				13129,"Hodremlin",
				13131,"Agav and Echio",
				13133,"Ragged Zombie",
				13135,"Zombie Slaughter",
				13137,"Banshee",
				13139,"Ferus ",
				13141,"Acidus";
			mes "[Melody-Jack]";
			mes "Nao tenho certeza se voce encontrou o que procura... Eu nao posso mais me envolver. Obrigado.";
		}
	}
	close;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp 900000,200000;
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		.@quest_id = getarg(.@i);
		switch( checkquest(.@quest_id,HUNTING) ) {
		case -1:
			if (checkquest(.@quest_id+1) > -1)
				erasequest (.@quest_id+1);
			continue;
		case 0:
		case 1:
			mes "[Melody-Jack]";
			mes getarg(.@i+1) +" e liberado. Nao ha necessidade de ter mais pedidos";
			break;
		case 2:
			mes "[Melody-Jack]";
			mes getarg(.@i+1) +" e liberado. Um acordo e um acordo. Eu preciso ser pago";
			getexp 900000,200000;
			break;
		}
		erasequest .@quest_id;
		next;
	}
	return;
OnTouch_:
	if (para_120lv01 == 1) {
		emotion ET_SURPRISE;
		mes "[Melody-Jack]";
		mes "Hey there! ... Wait.?";
		close;
	}
	end;
}

moc_para01,17,77,5	script	Aigu#1	4_F_IU,{
	mes "[Aigu]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Oh, por que voce nao faz parte do Grupo Eden?";
		mes "So estou falando com o nivel base ^FF0000121-130^000000 aventureiros";
	}
	else if (para_120lv01 == 0) {
		cutin "igu01",2;
		mes "Oi? Um belo dia como hoje. Posso pedir um favor? Ouvi dizer que os aventureiros do Grupo Eden sao simpaticos";
		next;
		if (select( "Eu nao sou do tipo simpatico", "Claro, eu ajudo." ) == 1) {
			cutin "igu05",2;
			mes "[Aigu]";
			mes "yek... Eu odeio";
		}
		else {
			cutin "igu03",2;
			mes "[Aigu]";
			mes "Serio? Eu vou me apresentar! Eu sou o chefe da banda ^ff0000Dandelion Aigu.^000000";
			next;
			mes "[Aigu]";
			mes "Acabo de voltar de uma apresentacao local. Ha alguem que eu queria conhecer, mas agora esta faltando, entao vim ao Grupo Eden para um pedido";
			next;
			mes "[Aigu]";
			mes "Decidi apenas fazer um pedido por mim mesmo porque ninguem esta no comando e todos estao ocupados. Obrigado pela ajuda";
			para_120lv01 = 1;
		}
	}
	else if (para_120lv01 > 0) {
		cutin "igu01",2;
		mes "E bom voltar a Midgard! Muito emocionante como o primeiro dia";
		next;
		if (select( "Qual e o seu pedido?", "Fale sobre outra coisa." ) == 2) {
			cutin "igu01",2;
			mes "[Aigu]";
			mes "A banda Dandelion viaja ao redor do mundo e transmite musica curativa para as pessoas";
			next;
			cutin "igu02",2;
			mes "[Aigu]";
			mes "Na verdade, nossas cancoes nao tem magica, mas tenho certeza de que ajuda as pessoas porque estou cantando";
		}
		else if (BaseLevel < 121) {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "Acho que e dificil para voce receber meu pedido? Por enquanto, acho que isso fara mais pessoas se perderem. Que tal ampliar sua visao viajando?";
		}
		else if (BaseLevel < 131) {
			mes "[Aigu]";
			mes "Acho que e dificil para voce receber meu pedido? Por enquanto, acho que isso fara mais pessoas se perderem. Que tal ampliar sua visao viajando?";
			next;
			switch( select( "Thanatos Tower search", "Rachel Sanctuary search", "Nameless Island search", "Abyss Lake search" ) ) {
				mes "[Aigu]";
			case 1:
				if (checkquest(13115,PLAYTIME) == 0)
					mes "Vou pedir para procurar novamente depois de algum tempo porque ha esperanca desde que foi encontrado um vestigio de alguem";
				else if (isbegin_quest(13111) == 1) {
					cutin "igu04",2;
					mes "Voce sabe que alguem foi la, mas nao sabe quem e esse alguem? Obrigado. Mas deixe-me pedir-lhe que procure novamente amanha, pois nao sabemos quem e";
					callsub S_Quest,13111,13115,1250000,300000;
				}
				else if (isbegin_quest(13107) == 1)
					mes "Voce pode encontrar abrigo no topo da torre. Mesmo aquele abrigo so tem agua e uma fogueira";
				else {
					mes "Os aventureiros se perdem facilmente por causa dos monstros perigosos e da altura da torre dos thanatos. Mas isso nao e impossivel para alguns aventureiros, entao, por favor, verifique por mim";
					next;
					if (select( "Comecar a procurar", "Cancelar." ) == 1) {
						mes "[Aigu]";
						mes "A torre Thanatos fica ao norte da Republica Schwarzwald! Nao esta longe de Juno ou Hugel";
						callsub S_Quest,13115,13107;// Thanatos Tower Search
					}
				}
				break;
			case 2:
				if (checkquest(13116,PLAYTIME) == 0)
					mes "Buscar Raquel foi suficiente. Voltarei a lhe perguntar depois de um tempo";
				else if (isbegin_quest(13112) == 1) {
					mes "Como foi sua viagem? Nenhuma noticia especial. Mas ainda nao esta feito, por isso lhe pedirei para procurar novamente";
					callsub S_Quest,13112,13116,1000000,300000;
				}
				else if (isbegin_quest(13108) == 1)
					mes "Nao podendo entrar no santuario de uma forma habitual. Nao e facil, mas podemos encontrar uma pista em Rachel";
				else {
					cutin "igu03",2;
					mes "Ha um santuario dentro do Santuario de Raquel. E possivel ficar preso la quando se esta la dentro";
					next;
					mes "[Aigu]";
					mes "Por favor, de uma olhada se houver algum vestigio de aventureiros perdidos na parte sudeste do 2º andar na zona do meio";
					next;
					if (select( "Comece a busca", "Pare" ) == 1) {
						cutin "igu05",2;
						mes "[Aigu]";
						mes "Desejo-lhe seguranca em sua busca";
						callsub S_Quest,13116,13108;// Rachel holy ground Search
					}
				}
				break;
			case 3:
				if (checkquest(13117,PLAYTIME) == 0)
					mes "A busca da Ilha Sem Nome continuara em outro momento. Que tal descansar um pouco?";
				else if (isbegin_quest(13113) == 1) {
					mes "Obrigado! Ha aventureiros errantes que tambem ajudam! Devemos continuar a procurar de qualquer forma. Por favor, ajude-me novamente da proxima vez";
					callsub S_Quest,13113,13117,900000,300000;
				}
				else if (isbegin_quest(13109) == 1) {
					cutin "igu04",2;
					mes "Talvez... andar por ai e beber com os mortos. Mas, por favor, fale comigo";
				}
				else {
					cutin "igu03",2;
					mes "Sim, talvez eles nao possam vir porque foram capturados pelos mortos! Ilha sem nome estara bem! Por favor, procurem na Ilha Sem Nome";
					next;
					if (select( "Comece a busca", "Pare" ) == 1) {
						cutin "igu01",2;
						mes "[Aigu]";
						mes "Peco-lhes que revistem o 1º subsolo do mosteiro na Ilha Sem Nome. Nao posso fazer voce correr esse risco, a nao ser que seja voluntario";
						callsub S_Quest,13117,13109;// Unknown island Search
					}
				}
				break;
			case 4:
				if (checkquest(13118,PLAYTIME) == 0)
					mes "Tentarei revistar o Abismo Lake depois de um tempo";
				else if (isbegin_quest(13114) == 1) {
					cutin "igu05",2;
					mes "Voce voltou em seguranca, mas acho que nao houve um bom resultado para seu rosto. Vou procurar mais, portanto nao decepcione! Obrigado";
					callsub S_Quest,13114,13118,900000,200000;
				}
				else if (isbegin_quest(13110) == 1) {
					cutin "igu05",2;
					mes "Talvez, decepcionado e caido porque um monstro nao foi encontrado no chao do lago";
				}
				else {
					mes "Sugiro procurar no Lago Abismo precisamente a caverna no meio do lago";
					next;
					mes "[Aigu]";
					mes "Sim! O fim de uma boa aventura precisa de um dragao! Ouvi dizer que ha um monstro no chao de um lago";
					next;
					if (select( "Begin the search.", "Stop." ) == 1) {
						mes "[Aigu]";
						mes "Abyss Lake e a maneira mais rapida de chegar a Hugel. Por favor, visite o 3º andar da caverna subterranea!";
						callsub S_Quest,13118,13110;// Abyss Lake Search
					}
				}
			}
		}
		else {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "Nao posso lhe dar mais tarefas de busca agora mesmo! Voce precisa de desafios mais dificeis, adequados ao seu nivel";
			callsub S_Erasequest,
				13107,"Torre de Thanatos",
				13108,"Santuario de Raquel",
				13109,"Ilha sem nome",
				13110,"Lago Abissal";
			callsub S_Erasequest2,
				13111,"Torre de Thanatos",1250000,300000,
				13112,"Santuario de Raquel",1000000,300000,
				13113,"Ilha sem nome",900000,300000,
				13114,"Lago Abissal",900000,200000;
			for ( .@i = 13115; .@i <= 13118; .@i++ )
				if (isbegin_quest(.@i)) erasequest .@i;
		}
	}
	close2;
	cutin "",255;
	end;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp getarg(2),getarg(3);
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "Oh, voce tem trabalhado em "+ getarg(.@i+1) +" Tarefa de aventura. Deixe-me limpar seu registro, para que voce possa trabalhar em tarefas melhores";
			erasequest getarg(.@i);
		}
	}
	return;
S_Erasequest2:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 4 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "Oh, voce ja terminou "+ getarg(.@i+1) +" Aventura. Obrigado por me ajudar com esta tarefa";
			erasequest getarg(.@i);
			getexp getarg(.@i+2),getarg(.@i+3);
		}
	}
	return;
}

-	script	Trace of an adventurer	-1,{
	.@quest = 13106 + atoi(strnpcinfo(2));
	.@delay = 13110 + atoi(strnpcinfo(2));
	if (isbegin_quest(.@quest) == 1) {
		mes "Encontrei vestigios de alguem que estava aqui, mas nao consigo encontrar uma pista. Vamos voltar e nos reportar a Aigu";
		erasequest .@quest;
		setquest .@delay;
	}
	else
		mes "Ha vestigios de alguem estar aqui";
	close;
}
thana_step,167,292,5	duplicate(Trace of an adventurer)	Trace of an adventurer#1	4_TRACE
ra_san05,265,69,5	duplicate(Trace of an adventurer)	Trace of an adventurer#2	4_TRACE
abbey02,155,290,5	duplicate(Trace of an adventurer)	Trace of an adventurer#3	4_TRACE
abyss_03,102,140,5	duplicate(Trace of an adventurer)	Trace of an adventurer#4	4_TRACE

tha_t06,126,128,3	script	Suspicious Monster#1	4_DEVIRUCHI,{
	mes "[Disguised Adventurer]";
	mes "So estou trabalhando neste lugar perigoso por 5000 zeny... Voce e o aventureiro que recebeu um pedido do Grupo Eden?";
	next;
	switch( select( "Yes","No" ) ) {
		mes "[Aventureiro disfarcado]";
	case 1:
		if (BaseLevel < 121 || BaseLevel > 130) {
			mes "Eu podia ver que nao e voce a primeira vista";
			close;
		}
		mes "Os aventureiros que procedem ao pedido de Aigu sao empenados para o nivel superior em nome de Aigu";
		next;
		if (select( "Go up.", "Do not go up." ) == 1) {
			mes "[Disguised Adventurer]";
			if (isbegin_quest(13107) != 1)
				mes "Parece que atualmente voce nao esta fazendo o pedido de aventura da Torre de Thanatos. Sinto muito, mas estou fazendo isso por Aigu e nao por voce";
			else {
				mes "Tenha cuidado! Voce nao pode descer depois de subir. Para Aigu!";
				close2;
				warp "thana_step",69,369;
				end;
			}
		}
		close;
	case 2:
		mes "Ou, por favor, finja nao me conhecer! Estarei em perigo se voce estragar meu disfarce!";
		close;
	}
}
