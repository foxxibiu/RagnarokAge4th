//===== rAthena Script =======================================
//= Eden Group Quests - Mission [71 - 85]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 71 - 85.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official scripts. [Euphy]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,38,3	script	Mission [71 - 85]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Apenas membros do -";
		mes "- O Grupo Eden pode ler -";
		mes "- este quadro de boletim. -";
		close;
	}

	// Clear the original quest data (no longer used).
	if (slv_quest) set slv_quest,0;
	if (checkquest(10102) > -1 || checkquest(10103) > -1 || checkquest(10104) > -1 || checkquest(10105) > -1 || checkquest(10106) > -1) {
		for(set .@quest,10102; .@quest<=10106; set .@quest,.@quest+1) {
			if (checkquest(.@quest) > -1)
				erasequest .@quest;
		}
		mes "- Desde que o cliente se mudou -";
		mes "- para outro lugar, -";
		mes "- o formulário de candidatura foi retirado. -";
		next;
	}

	if (BaseLevel < 71) {
		mes "- Apenas membros do -";
		mes "- nível direito pode ler -";
		mes "- este quadro de boletim. -";
		mes "- tentar encontrar um quadro -";
		mes "- - que se adequa ao seu nível. -";
		close;
	} else if (BaseLevel > 85) {
		// Quest IDs: 10107-10123, 5055-5057
		setarray .@quests[0],
			10107,10108,10109,
			10110,10111,10112,10113,
			10114,10115,10116,10117,
			10118,10119,10120,
			10121,10122,10123,5055,5056;
		for(set .@i,0; .@i<getarraysize(.@quests); set .@i,.@i+1) {
			if (checkquest(.@quests[.@i],HUNTING) == 2)
				set .@complete[getarraysize(.@complete)], .@quests[.@i];
		}
		if (checkquest(5057) > -1 && countitem(7187) >= 30)
			set .@complete[getarraysize(.@complete)],5057;
		if (getarraysize(.@complete)) {
			mes "Tens missões em curso";
			mes "Quer entregá-las?";
			next;
			if(select("Claro.:Não.") == 2)
				close;
			for(set .@i,0; .@i<getarraysize(.@complete); set .@i,.@i+1)
				callsub L_Quest,.@complete[.@i];
			close;
		}
		mes "- Apenas membros do -";
		mes "- nível direito pode ler -";
		mes "- este quadro de boletim. -";
		mes "- tentar encontrar um quadro -";
		mes "- - que se adequa ao seu nível. -";
		close;
	}
	mes "- Escolha onde está -";
	mes "- gostaria de caçar. -";
	next;
	switch(select("Calabouço da Esfinge:Glast Heim:Juno Área:Torre do Relógio:Ilhas Localizadas")) {
	case 1:
		mes "- Há vários pedidos -";
		mes "- relacionados com a Masmorra da Esfinge -";
		mes "- neste quadro de missão. -";
		next;
		set .@str1$,"Os aldeões de Marrocos estão preocupados com o número crescente de monstros na Masmorra da Esfinge. ";
		set .@str2$,"- Meidi from Morocc -";
		switch(select("Caça Requiem:Caçar Marduk:Caçar Pasana")) {
			case 1: callsub L_Quest,10107,.@str1$+"Caçar 10 Requiems.",.@str2$;
			case 2: callsub L_Quest,10108,.@str1$+"Caçar 10 Requiems.",.@str2$;
			case 3: callsub L_Quest,10109,.@str1$+"Caçar 10 Requiems.",.@str2$;
		}
	case 2:
		mes "- Há vários pedidos -";
		mes "- relacionados com Glast Heim -";
		mes "- neste quadro de missão. -";
		next;
		set .@str1$,"Tenho muito medo que os monstros de Glast Heim nos ataquem. ";
		set .@str2$,"- Ancélia de Geffen -";
		switch(select("Caçar Moldura Escura:Caçar Druida Maléfico:Caçar Wraith:Hunt Raydric Arqueiro")) {
			case 1: callsub L_Quest,10110,.@str1$+"Caça 10 Quadros Escuros.",.@str2$;
			case 2: callsub L_Quest,10111,.@str1$+"Caçar 10 druidas malvados.",.@str2$;
			case 3: callsub L_Quest,10112,.@str1$+"Caça 10 Wraiths.",.@str2$;
			case 4: callsub L_Quest,10113,.@str1$+"Cala 10 Raydric Arqueiro.",.@str2$;
		}
	case 3:
		mes "- Há vários pedidos -";
		mes "- relacionados com a área -";
		mes "- - em torno de Juno. -";
		next;
		set .@str1$,"Aqueles que não podem lutar, como eu, têm muito mais dificuldade em viajar devido ao grande número de monstros. ";
		set .@str2$,"- Jeanbai, traveler -";
		switch(select("Caçar Grand Peco:Caçar Dorminhoco:Caça ao bode:Caça Harpia")) {
			case 1: callsub L_Quest,10114,.@str1$+"Caçar 20 Grand Peco.",.@str2$;
			case 2: callsub L_Quest,10115,.@str1$+"Caçar 20 Dorminhoc.",.@str2$;
			case 3: callsub L_Quest,10116,.@str1$+"Caça 20 bode.",.@str2$;
			case 4: callsub L_Quest,10117,.@str1$+"Caça 20 Harpia.",.@str2$;
		}
	case 4:
		mes "- Há vários pedidos -";
		mes "- relacionados com a Torre do Relógio -";
		mes "- neste quadro de missão. -";
		next;
		set .@str1$,"Eu adorava ouvir a doce melodia da Torre do Relógio Al De Baran, mas já não consigo ouvir nada devido a todos os ruídos que os monstros estão a fazer. ";
		set .@str2$,"- Rizingsetter, Presidente da comunidade do Relógio do Amor -";
		switch(select("Caça Relógio:Caçar Punk:Caça Rideword")) {
			case 1: callsub L_Quest,10118,.@str1$+"Caçar 15 Relógios antes de partir.",.@str2$;
			case 2: callsub L_Quest,10119,.@str1$+"Caçar 15 Punks antes de partir.",.@str2$;
			case 3: callsub L_Quest,10120,.@str1$+"Caçar 15 Ridewords antes de quebrar.",.@str2$;
		}
	case 5:
		mes "- Há vários pedidos -";
		mes "- relacionados com as Ilhas Localizadas -";
		mes "- nesta comissão de missão. -";
		next;
		set .@str1$,"Quero partir para uma aventura, mas há tantas coisas com que tenho de lidar. Se eu não posso, então outros também não podem! ";
		set .@str2$,"- Funfy, quem quer partir para uma aventura -";
		switch(select("Caçar Kikimora:Caçar Boneca Miyabi:Caçar Mi Gao:Caça à Mula sem Cabeça:Caçar Tamruan:Reunir Máscaras do Festival")) {
			case 1: callsub L_Quest,10121,.@str1$+"Caça 15 Kikimoras em Moscovia!",.@str2$;
			case 2: callsub L_Quest,10122,.@str1$+"Caça 15 Bonecas Miyabi em Amatsu!",.@str2$;
			case 3: callsub L_Quest,10123,.@str1$+"Caçar 15 Mi Gaos em Luoyang!",.@str2$;
			case 4: callsub L_Quest,5055,"Sei que há muitos monstros aterradores fora da cidade, mas o que me aterroriza são estas Toupeiras sem Cabeça à espreita. Espero que eles nunca se aventurarão na cidade. Caçar 15 Toupeiras sem Cabeça!","- Mariana de Brasilis -";
			case 5: callsub L_Quest,5056,"Os tamruanos fazem muito barulho. Não me importo de manhã, mas eles impedem-me de adormecer à noite. Eu quero dormir. Caçar 15 Tamruans!","- Shaman Pumo de Ayothaya -";
			case 6: callsub L_Quest,5057,"Os Dumpling Childs roubaram todas as máscaras que eu tinha preparado para o festival. Tentem obter 30 máscaras! Ficar-lhe-ei a dever~","- SaYumMoon Chefe de Kunlun -";
		}
	}
	end;

//callsub L_Quest,<quest ID>{,"<description1>","<description2>"};
L_Quest:
	set .@quest, getarg(0);
	switch(.@quest) {
		case 10107: set .@name$,"Caçar Requiem"; set .@exp,15000; break;
		case 10108: set .@name$,"Caçar Marduk"; set .@exp,15000; break;
		case 10109: set .@name$,"Caçar Pasana"; set .@exp,15000; break;
		case 10110: set .@name$,"Caçar Moldura Escura"; set .@exp,20000; break;
		case 10111: set .@name$,"Caçar Druida Maléfico"; set .@exp,20000; break;
		case 10112: set .@name$,"Caçar Wraith"; set .@exp,20000; break;
		case 10113: set .@name$,"Caçar Raydric Arqueiro"; set .@exp,20000; break;
		case 10114: set .@name$,"Caçar Grande Peco"; set .@exp,30000; break;
		case 10115: set .@name$,"Caçar Dorminhoco"; set .@exp,30000; break;
		case 10116: set .@name$,"Caçar Cabra"; set .@exp,30000; break;
		case 10117: set .@name$,"Caçar Harpia"; set .@exp,30000; break;
		case 10118: set .@name$,"Caçar Relógio"; set .@exp,25000; break;
		case 10119: set .@name$,"Caçar Punk"; set .@exp,25000; break;
		case 10120: set .@name$,"Caçar Rideword"; set .@exp,25000; break;
		case 10121: set .@name$,"Caçar Kikimora"; set .@exp,30000; break;
		case 10122: set .@name$,"Caçar Boneca Miyabi"; set .@exp,30000; break;
		case 10123: set .@name$,"Caçar Mi Gao"; set .@exp,30000; break;
		case 5055: set .@name$,"Caçar Mula sem cabeça"; set .@exp,30000; break;
		case 5056: set .@name$,"Caçar Tamruan"; set .@exp,30000; break;
		case 5057: set .@name$,"Reunir Máscaras do Festival"; set .@exp,30000; break;
	}
	if (getargcount() == 1) {
		mes "'"+.@name$+"' concluído. Quer entregá-lo e receber a sua recompensa?";
		next;
		if(select("Turn in quest.:Cancelar.") == 1) {
			if (.@quest == 5057) delitem 7187,30; //Festival_Mask
			erasequest .@quest;
			getexp .@exp,0;
			mes "Recebeu EXP para terminar a busca.";
			next;
			return;
		}
		close;
	} else {
		if (.@quest == 5057) {
			// Quest 5057 is the only collection quest, so separate conditions are unnecessary.
			if (checkquest(5057) == -1)
				set .@hunting,-1;
			else if (countitem(7187) >= 30)
				set .@hunting,2;
		} else
			set .@hunting, checkquest(.@quest,HUNTING);
		if (.@hunting == 0 || .@hunting == 1) {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "Volte aqui quando tiver terminado a busca.";
		} else if (.@hunting == 2) {
			mes "Concluiu a busca.";
			next;
			if(select("Turn in quest.:Cancel.") == 1) {
				if (.@quest == 5057) delitem 7187,30; //Festival_Mask
				erasequest .@quest;
				getexp .@exp,0;
				mes "Recebeu EXP para terminar a busca.";
			}
		}/* else if (.@hunting == 3) {
//			recall_completequest .@quest
			erasequest .@quest;
			mes "You have re-accepted the quest '"+.@name$+"'.";
		}*/ else {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "Quer aceitar a demanda?";
			next;
			if(select("Sim.:Não.") == 1) {
				mes "Recebeu a demanda '"+.@name$+"'. Veja as informações da sua busca para mais detalhes.";
				setquest .@quest;
			}
		}
		close;
	}
}
