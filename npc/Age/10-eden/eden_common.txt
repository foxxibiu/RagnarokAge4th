//===== rAthena Script ======================================= 
//= Eden Group Quests - Common NPCs
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Eden Group Headquarter NPC's.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Eden Group Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 Added support to Para_Team_Mark_ in Eden Group. [AtlantisRO/Ragno]
//= 2.0 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

// Common NPCs :: lv_tre
//============================================================
moc_para01,27,35,5	script	Secretary Lime Evenor	4_F_EDEN_OFFICER,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- demasiados itens com você. -";
		mes "- Por favor, tente novamente -";
		mes "- depois que você perder algum peso. -";
		close;
	}
	if (countitem(6219) > 0) {
		mes "[Lime Evenor]";
		mes "Graças a muitos aventureiros que visitaram nosso Grupo Eden, preparamos recentemente uma nova versão da Marca do Grupo Eden para nossos visitantes";
		mes "E também é possível trocar a antiga versão da Marca do Grupo do Éden por uma nova versão";
		next;
		mes "[Lime Evenor]";
		mes "Embora tenha um pequeno atraso, esta nova versão do Eden Group Mark o enviará de volta ao local atual salvo";
		mes "Você gostaria de trocar o seu?";
		next;
		if (select("Sure:I'm good.") == 2){
			mes "[Lime Evenor]";
			mes "Que motivo você teria... para não trocar?";
			close;
		}
		delitem 6219,1;
		getitem 22508,1;
		mes "[Lime Evenor]";
		mes "Aí está. Volte novamente~";
		close;
	}
	mes "[Lime Evenor]";
	mes "As pessoas que seguem seus sonhos e romances me escutam. Somos representantes do paraíso chamado O Jardim do Éden, chamado grupo do Éden. Não há lugar como este em nenhum lugar";
	mes "Olá. Posso ajudá-los?";
	next;
	while (1) {
		switch (select("O que é o Grupo Eden?:Junte-se ao Grupo Eden.:Registrar nova missão.:Procurar por missões.:Cancelar.")) {
		case 1:
			mes "[Lime Evenor]";
			mes "O Grupo Eden é um grupo que ajuda as pessoas que estão sofrendo de angústias em Midgard";
			next;
			mes "[Lime Evenor]";
			mes "Se as pessoas precisarem de ajuda, colocamos o caso no quadro de avisos e o apresentamos aos nossos membros que estão ansiosos para ajudar os necessitados";
			next;
			mes "[Lime Evenor]";
			mes "Os membros do Éden revisarão as missões e ajudarão aqueles que as postarem";
			mes "Os membros fazem estas missões por recompensas";
			next;
			mes "[Lime Evenor]";
			mes "Se você não está muito familiarizado com Midgard, esta é uma ótima maneira de explorar o mundo";
			mes "Ou, se você for maduro o suficiente, você pode viajar pelo mundo e fazer novos amigos enquanto faz missões. Seu honorável nome será divulgado entre as terras";
			next;
			break;
		case 2:
			if (!callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "Você pode ser um membro do Éden simplesmente registrando-se comigo";
				mes "Você gostaria de se juntar ao Grupo do Éden?";
				next;
				switch (select("Sim, eu quero participar.:Não, eu não quero participar.")) {
				case 1:
					mes "[Lime Evenor]";
					mes "Aright. Excelente! Por favor, escreva seu nome aqui";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Você terminou?";
					mes "Deixe-me ver.";
					next;
					mes "[Lime Evenor]";
					mes "Seu nome é ^3131FFBo"+.@input$+"Ba^000000...? É isso mesmo?";
					mes "Não é???";
					next;
					emotion ET_SWEAT;
					mes "[Lime Evenor]";
					mes "Hmm, não foi isso que você escreveu?";
					mes "Ok, hmm, parece um pouco difícil de ler";
					next;
					mes "[Lime Evenor]";
					mes "Você gostaria de escrever seu nome novamente? Por favor, escreva ^3131FFClearly^000000";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Ah~ ha. Você é ^3131FF "+strcharinfo(0)+"^000000";
					mes "Eu acertei desta vez";
					next;
					emotion ET_BEST;
					mes "[Lime Evenor]";
					mes "Nós já colocamos seu nome na lista";
					mes "Bem-vindo ao grupo do Éden, novo membro!";
					next;
					mes "[Lime Evenor]";
					mes "Espero que você possa fazer um grande trabalho como membro do Éden";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes "[Lime Evenor]";
					mes "Você ainda tem perguntas sobre o Grupo Eden?";
					next;
					break;
				}
			} else {
				mes "[Lime Evenor]";
				mes "Você já é um membro do Grupo Eden";
				next;
			}
			break;
		case 3:
			mes "[Lime Evenor]";
			mes "Você quer registrar algumas missões que você quer que façamos?";
			next;
			if(select("Sim, eu quero me registrar.:Não, não quero.") == 1) {
				mes "[Lime Evenor]";
				mes "Muito bem. Por favor, escreva seu nome nele";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000.. É esse o seu nome?";
				mes "Hmm, sua caligrafia não é clara. Eu não consigo ler";
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000, Diga-me para que você quer se registrar";
				mes "Para qual mapa devemos ir?";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMapa da missão: "+.@inputstr$+"^000000";
				mes "Hum, eu vou notar isso";
				next;
				mes "[Lime Evenor]";
				mes "Por favor, me informe que tipo de missões você está procurando";
				mes "Deve ser brevemente como 'Hunt 10 Porings'";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "Você terminou?";
				mes "Deixe-me verificar.";
				next;
				mes "[Lime Evenor]";
				mes "Huh...........";
				next;
				mes "[Lime Evenor]";
				mes "Hum.. huh??..................";
				next;
				emotion ET_THINK;
				mes "[Lime Evenor]";
				mes "Acho que você deve completar uma de nossas missões primeiro antes de assumir uma nova missão";
				next;
				emotion ET_HNG;
				mes "[Lime Evenor]";
				mes "^3131FFClient: Lime Evenor^000000";
				mes "^3131FFMission: Pratique sua caligrafia por um mês. ^000000";
				mes "Você tem uma caligrafia muito ruim!";
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMission:"+.@input$+"^000000";
				mes "De qualquer forma, você está pronto para se registrar para uma nova missão";
				next;
				mes "[Lime Evenor]";
				mes "As missões estão totalmente reservadas. Você tem que esperar por um lugar para limpar";
				mes "Por favor, espere até sua vez";
				next;
			}
			break;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "Gostaria de tentar algumas missões como um membro do Éden?";
				next;
				mes "[Lime Evenor]";
				mes "Verifique o lado direito do quadro de avisos, há vários tipos de missões esperando por você";
				mes "Leia atentamente a lista de missões e as qualificações e escolha uma delas";
				next;
			}
			else {
				mes "[Lime Evenor]";
				mes "Você precisa se juntar ao Grupo Eden primeiro se você quiser fazer algumas missões";
				close;
			}
			break;
		case 5:
			mes "[Lime Evenor]";
			mes "Você precisa se juntar ao Grupo Eden primeiro se você quiser fazer algumas missões";
			close;
		}
	}
	close;
}

-	script	Eden Teleport Officer#0::eto	-1,{
	cutin "ep172_barmund_b03",2;
	mes "[Eden Teleport Officer]";
	mes "Aqueles que vagueiam por este estranho mundo com olhos sem rumo, escutem e venham até mim";
	mes "O grupo Eden está aqui para ajudá-los e lhes mostrará o grande futuro";
	next;
	mes "[Eden Teleport Officer]";
	mes "Você não tem nada a fazer a não ser perder seu tempo...";
	mes "Você está ansioso para fazer algo bom mas ninguém lhe oferece trabalho...";
	mes "Você gostaria de ser um solucionador de problemas?";
	mes "O grupo Eden está aqui para resolver seu problema";
	next;
	switch (select("Mudar-se para o Grupo Eden.:Não quero mais falar.")) {
	case 1:
		mes "[Eden Teleport Officer]";
		mes "Vamos para nossa base secreta!";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[Eden Teleport Officer]";
		mes "Se você não sabe o que fazer para o seu futuro, não hesite em me dizer. Basta passar por aqui e falar comigo! Ok?";
		cutin "",255;
		close;
	}
}
prontera,145,174,5	duplicate(eto)	Eden Teleport Officer#1	10351
moc_ruins,68,164,3	duplicate(eto)	Eden Teleport Officer#2	10351
geffen,132,66,3	duplicate(eto)	Eden Teleport Officer#3	10351
alberta,180,140,3	duplicate(eto)	Eden Teleport Officer#4	10351
aldebaran,136,116,5	duplicate(eto)	Eden Teleport Officer#5	10351
izlude_in,68,162,1	duplicate(eto)	Eden Teleport Officer#6	10351
prt_church,103,78,3	duplicate(eto)	Eden Teleport Officer#7	10351
geffen_in,160,104,5	duplicate(eto)	Eden Teleport Officer#8	10351
moc_prydb1,53,126,3	duplicate(eto)	Eden Teleport Officer#9	10351
alberta_in,75,39,3	duplicate(eto)	Eden Teleport Officer#10	10351
payon_in02,58,58,1	duplicate(eto)	Eden Teleport Officer#11	10351
payon,165,232,3	duplicate(eto)	Eden Teleport Officer#12	10351
que_ng,33,73,3	duplicate(eto)	Eden Teleport Officer#13	10351
que_ng,144,161,5	duplicate(eto)	Eden Teleport Officer#14	10351
yuno,144,188,5	duplicate(eto)	Eden Teleport Officer#15	10351
rachel,125,144,3	duplicate(eto)	Eden Teleport Officer#16	10351
comodo,202,151,4	duplicate(eto)	Eden Teleport Officer#17	10351
hugel,93,153,4	duplicate(eto)	Eden Teleport Officer#18	10351
veins,220,109,4	duplicate(eto)	Eden Teleport Officer#19	10351
einbroch,250,211,4	duplicate(eto)	Eden Teleport Officer#20	10351
lighthalzen,164,86,4	duplicate(eto)	Eden Teleport Officer#21	10351
amatsu,204,97,4	duplicate(eto)	Eden Teleport Officer#22	10351
ayothaya,143,110,4	duplicate(eto)	Eden Teleport Officer#23	10351
louyang,224,107,3	duplicate(eto)	Eden Teleport Officer#24	10351
gonryun,163,119,4	duplicate(eto)	Eden Teleport Officer#25	10351
moscovia,228,191,5	duplicate(eto)	Eden Teleport Officer#26	10351
brasilis,191,224,3	duplicate(eto)	Eden Teleport Officer#27	10351
dewata,192,193,5	duplicate(eto)	Eden Teleport Officer#28	10351
morocc,161,97,5	duplicate(eto)	Eden Teleport Officer#29	10351
izlude,135,152,4	duplicate(eto)	Eden Teleport Officer#30	10351
izlude_a,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_a	10351
izlude_b,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_b	10351
izlude_c,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_c	10351
izlude_d,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_d	10351
umbala,105,158,3	duplicate(eto)	Eden Teleport Officer#31	10351
malaya,225,218,1	duplicate(eto)	Eden Teleport Officer#32	10351
verus04,116,243,0	duplicate(eto)	Eden Teleport Officer#33	10351
jawaii,118,188,5	duplicate(eto)	Eden Teleport Officer#34	10351

moc_para01,30,10,0	script	#eden_out	45,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",156,191; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;	// Old coordinates: (127,142)
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		case 33: warp "verus04",115,243; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	111,{
	if (callfunc("F_HasEdenGroupMark")) {
		mes "[Lime Evenor]";
		mes "Oh, é um lugar exclusivo apenas para os membros do Éden";
		mes "Se você é um membro, você pode vir quando quiser!";
		next;
		switch (select("Entre.:Não Entre.")) {
		case 1:
			mes "Esta porta é lindamente decorada, mas parece um pouco pesada demais";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes "[Lime Evenor]";
			mes "Bem, se você não está interessado";
			close;
		}
	}
	mes "[Lime Evenor]";
	mes "Oh, este é um lugar exclusivo apenas para os membros do Éden";
	mes "Se você quer entrar, você tem que se juntar ao Grupo do Éden";
	close;
}

moc_para01,16,22,7	script	Old Adventurer#eden	900,{
	mes "[Old Adventurer]";
	mes "Duro, eu sou forte o suficiente mesmo que pareça velho! Qual é o nome dela? Lime Evenor? Ela simplesmente me ignora como se eu fosse uma espécie de velho senil bastardo";
	next;
	mes "[Old Adventurer]";
	mes "Eu vim aqui para me juntar ao grupo Eden! Eu não mereço isto";
	mes "Você também acha que eu pareço senil?";
	close;
}

moc_para01,182,48,3	script	Eden's Chief#eden	886,{
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes "[Eden's Chief]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion ET_CRY;
	mes "[Eden's Chief]";
	mes "Oh, caramba! Quantos ovos eu já fiz?";
	mes "Aww!!! Eu esqueci totalmente! Tenho que contar de novo!";
	next;
	emotion ET_CRY;
	mes "[Eden's Chief]";
	mes "Por que você me pediu para fazer isso? Que tipo de missão é essa?";
	next;
	emotion ET_CRY;
	mes "[Eden's Chief]";
	mes "O que você vai fazer com todos esses ovos fritos? Tenho certeza de que você não pode comer todos eles. Você só quer me incomodar, certo?";
	close;
}

moc_para01,172,28,5	script	Eden's Intern#eden	883,{
	mes "[Eden's Intern]";
	mes "Na verdade, eu percebi isso";
	next;
	mes "[Eden's Intern]";
	mes "Temos que trabalhar muito, a menos que você queira falhar";
	next;
	mes "[Eden's Intern]";
	mes "Mas, eu nunca aprendi sobre produtos farmacêuticos ainda";
	next;
	mes "[Eden's Intern]";
	mes "Eu vou falhar. Não posso fazer isto";
	close;
}

moc_para01,20,35,5	script	Office Assistant Neede	814,{
	mes "[Neede]";
	mes "Tornei-me membro do Éden há vários anos, mas parece que foi ontem";
	mes "Tenho feito muito trabalho, então nem notei quantos anos passei aqui";
	next;
	mes "[Neede]";
	mes "Lembra-me que era como uma guerra todos os dias";
	next;
	mes "[Neede]";
	mes "O chefe do restaurante ^3131FF do lado direito^000000";
	mes "usa muito tempero toda vez que cozinha, o cheiro está em todo o saguão. Temos que abrir a janela regularmente porque não suportamos o cheiro";
	next;
	mes "[Neede]";
	mes "E, você vê o ^3131FFgymnasium^000000 na cruz do ^3131FF o lado norte do lobby^000000? Eles sempre gritam quando se exercitam, eu posso até ouvi-los daqui";
	next;
	emotion ET_ANGER;
	mes "[Neede]";
	mes "Além disso, a estranha garota do segundo andar ^3131FF000000 está chorando o tempo todo. Os gritos dela me deixam louco";
	next;
	mes "[Neede]";
	mes "Ah, aquela garota estranha é na verdade nossa chefe...";
	next;
	mes "[Neede]";
	mes "Eu não sei o que ela tem feito. Mas ela chora e grita o tempo todo. Oh, às vezes ouço 'Bang!', sons estrondosos... Eu não sei de onde ela veio";
	next;
	emotion ET_HUK;
	mes "[Neede]";
	mes "Wait!";
	mes "Shh! É um segredo, não se pode contar a ninguém!";
	next;
	mes "[Neede]";
	emotion ET_OHNO;
	mes "Eu não confio em Evenor. Ela nunca mostra seus sentimentos... ela vai me denunciar ao meu chefe";
	next;
	emotion ET_HNG, getnpcid(0, "Secretário Lime Evenor");
	mes "[Lime Evenor]";
	mes "Eu não sou esse tipo de garota";
	next;
	emotion ET_KEK;
	mes "[Neede]";
	mes "Huh? Você ouviu isso? Caramba!";
	close;
}

function	script	F_HasEdenGroupMark	{
	return (countitem(6219) > 0 || countitem(22508) > 0);
}
