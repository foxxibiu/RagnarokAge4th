// Original script by Unknown
// Edit by Mabuhay

-	script	Event_NvZ	-1,{
OnInit:
	// item reward
	setarray .item,
		480083, 10,
		480083, 2;

	// @nvzstart - gm manual start
	bindatcmd "nvzstart", strnpcinfo(0)+"::OnStart",60,60;
	
	// @nvz - player join event
	bindatcmd "nvz", strnpcinfo(0)+"::OnJoinEvent";
	
	// Event cooldown
	.event_cd = 60 * 60 * 2; // 1hr
	
	// Mapflags
	setarray .@mapflag,
		mf_nowarp,
		mf_nowarpto,
		mf_noskill,
		mf_noteleport,
		mf_nomemo,
		mf_nosave,
		mf_noicewall,
		mf_nobranch,
		mf_noreturn,
		mf_nocommand;
		
	for ( .@i = 0; .@i < getarraysize(.@mapflag); .@i++)
		setmapflag "quiz_01", .@mapflag[.@i];
		
	.@min = gettime(DT_MINUTE)*60;
	.@sec = gettime(DT_SECOND);
	.event_time = (gettimetick(2) - ( .@min + .@sec )) + .event_cd - 5;
	end;
	
OnJoinEvent:
	mes "[Aprendiz x Zumbi]";
	if ( !.start ) mes "Próxima rodada irá acontecer ^FF0000"+ Time2Str(.event_time) +"^000000.";
	if ( .start == 1 ) mes "Evento está atualmente ativo.";
	if ( .start == 2 ) mes "Evento está acontecendo agora.";
	next;
	switch(select(
		"Aprendiz x Zumbi - "+ ((.start == 1) ? "^00ff00Participar":((.start == 2) ? "^00ff00O evento está em andamentol":"^ff0000Não Disponível"))+"^000000" )) {
	case 1:
		if ( !.start ) {
			mes "O evento não está disponível";
			close;
		} else if ( .start == 2 ) {
			mes "O evento está em andamento";
			close;
		}
		if ( BaseLevel > 1 || Class != 0 ) {
			mes "Apenas Aprendizes Nível 1 podem entrar.";
			close;
		}
		else {
			getinventorylist;
			if ( @inventorylist_amount ) {
				mes "Você não pode ter nenhum item no inventário.";
				close2;
				@inventorylist_amount = false;
				end;
			}
			sc_end SC_ALL;
			sc_start SC_DECREASEAGI,600000,10;
			percentheal 100,100;
			warp "quiz_01",42,369;
		}
	}
	end;

OnStart: .@force = true;
OnMinute00: // runs hourly
	.@time = gettimetick(2);
	.@min = gettime(DT_MINUTE)*60;
	.@sec = gettime(DT_SECOND);
	if ( .event_time > .@time && !.@force || .start && !.@force ) end; // doesnt start if cd isn't done.
	.event_time = (.@time - ( .@min + .@sec )) + .event_cd - 5;  
	.start = 1;
	killmonsterall "quiz_01";
	announce "NxZ: Teremos um evento Aprendiz x Zumbi.",0;
	sleep 10000;
	announce "NxZ: Para quem quiser entrar use @nvz",0;
	sleep 10000;
	announce "NxZ: Após 1 minuto, o evento será encerrado.",0;
	sleep 10000;
	announce "NxZ: Então, por favor, se você quiser participar. use @nvz",0;
	sleep 60000;
	announce "NxZ: A entrada para o evento já está fechada.",bc_all,0x0ceedb;
	if ( getmapusers("quiz_01") <= 1 ) {
		announce "NxZ: Não há participantes suficientes.",bc_all,0x0ceedb;
		.start = false;
		mapwarp "quiz_01", "prontera", 150, 180; // warp out.
		end;
	}
	.start = 2;
	sleep 3500;
	mapannounce "quiz_01","Estamos prestes a começar o evento.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "quiz_01","As regras são simples.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "quiz_01","Zumbis serão gerados no mapa.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "quiz_01","e aparecerá a cada 20 segundos.",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "quiz_01","Último sobrevivente vence.",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "quiz_01","Isso é tudo. Vamos começar.",bc_map,0x0ceedb;
	sleep 3000;
	monster "quiz_01",42,369,"Zombie",1036,4;
	initnpctimer;
	end;
	
OnTimer20000: // 20 seconds timer and also player check on how many left in map
	monster "quiz_01",42,369,"Zumbi",1036,10;
	setnpctimer 0;
	end;
	
OnPCDieEvent:
	if ( .start && strcharinfo(3) == "quiz_01" ) {
		warp "prontera",rand(148,163),rand(167,186);
		if ( getmapusers("quiz_01") == 1 ) {
			stopnpctimer;
			killmonsterall "quiz_01";
			.@size = getmapunits(BL_PC,"quiz_01",.@aid);
			.@amt = getarraysize(.item);
			.start = false;
			attachrid .@aid;
			announce strcharinfo(0)+" ganhou o evento Novice x Zumbi.",bc_all,0x0ceedb;
			for ( .@j = 0; .@j < .@amt; .@j += 2 )
				getitem .item[.@j], .item[.@j+1];				
			warp "prontera",rand(148,163),rand(167,186);
			end;
		}
	}
	end;
}