prontera,145,164,5	script	Banco Rekenber	4_M_REKENBER,{
cutin "ep15_rekenber02",2;
function int__;

do{
	callsub exchange_rate;
	.@menu = select( "Zeny por Moeda","Moeda por Zeny","Nada Obrigado!" );
	switch( .@menu ){
		Case 1:
			mes "Converter Zeny em qual moedas?";
			.@coin = select( implode( .coin_name$,":" ) ) - 1;
			if( Zeny <  .value[.@coin] ){
				mes "^FF0000[ERROR]^000000 Você não teve zeny suficiente.";
			}else{
				.@maximum = Zeny / .value[.@coin];
				mes "You can get maximum of ^FF0000"+int__( .@maximum )+" x "+getitemname( .coin_id[.@coin] )+"^000000";
				input .@amount,0,.@maximum;
				if( .@maximum )
					if( checkweight( .coin_id[.@coin],.@amount ) ){
						.@total = ( .@amount * .value[.@coin] );
						Zeny -= .@total;
						getitem .coin_id[.@coin],.@amount;
						message strcharinfo(0),"Exchanged "+int__( .@total )+" Zeny to "+int__( .@amount )+" "+getitemname( .coin_id[.@coin] )+"^000000";
					}else{
						mes "^FF0000[ERROR]^000000 Você não pode aguentar tanto.";
					}
			}
			break;
		Case 2:
			mes "Converter quais moedas em Zeny?";
			.@coin = select( implode( .coin_name$,":" ) ) - 1;
			if( !countitem( .coin_id[.@coin] ) ){
				mes "^FF0000[ERROR]^000000 Você não tem nenhuma "+getitemname( .coin_id[.@coin] )+".";
			}else if( ( .max_zeny - Zeny ) < .value[.@coin] ){
				mes "^FF0000[ERROR]^000000 Você não pode trocar esta moeda, pois ela atinge o limite Zeny.";
			}else{
				.@maximum = (( .max_zeny - Zeny ) / .value[.@coin] );
				if( .@maximum > countitem( .coin_id[.@coin] ) ) .@maximum = countitem( .coin_id[.@coin] );
				mes "Você pode trocar no máximo de ^FF0000"+int__( .@maximum )+" x "+getitemname( .coin_id[.@coin] )+"^000000 para zeny";
				input .@amount,0,.@maximum;
				if( .@maximum ){
					.@total = ( .@amount * .value[.@coin] );
					Zeny += .@total;
					delitem .coin_id[.@coin],.@amount;
					message strcharinfo(0),"Exchanged "+int__( .@amount )+" "+getitemname( .coin_id[.@coin] )+" to "+int__( .@total )+" Zeny^000000";
				}
			}
			break;
		default: break;
	}
	next;
}while( .@menu != 3 );
mes "Até mais.";
cutin "ep15_rekenber02",255;
close;

exchange_rate:
	mes "Taxa de Câmbio :";
	for( .@i = 0; .@i < .coin_size; .@i++ )
		mes "^777777 > "+.coin_name$[.@i]+" - "+int__( .value[.@i] )+" z^000000";
	return;
	
OnInit:
// server max zeny
.max_zeny = 2000000000;
// coin list + name + value
setarray .coin_name$,"Bronze","Prata","Ouro";
setarray .coin_id,673,675,671;
setarray .value,100000000,500000000,1000000000;

.coin_size = getarraysize( .coin_name$ );


// credits to annieruru
function	int__	{
	set .@num, atoi(""+getarg(0));
	if ( .@num == 0 || .@num >= 2147483647 ) return getarg(0);
	set .@l, getstrlen(""+.@num);
	for ( set .@i,0; .@i < .@l; set .@i, .@i + 1 ) {
		set .@num$, .@num % pow(10,.@i+1) / pow(10,.@i) + .@num$;
			if ( (.@i+1) % 3 == 0 && .@i+1 != .@l ) set .@num$, ","+ .@num$;
	}
	return .@num$;
}


}