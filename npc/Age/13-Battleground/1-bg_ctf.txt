// (c) 2008 - 2012 eAmod Project; Andres Garbanzo / Zephyrus
//
//  - gaiaro.staff@yahoo.com
//  - MSN andresjgm.cr@hotmail.com
//  - Skype: Zephyrus_cr
//  - Site: http://dev.terra-gaming.com
//
// This file is NOT public - you are not allowed to distribute it.
// Authorized Server List : http://dev.terra-gaming.com/index.php?/topic/72-authorized-eamod-servers/
// eAmod is a non Free, extended version of eAthena Ragnarok Private Server.

// ==============================================================================
// BattleGround System - Flavius Capture the Flag
// ==============================================================================

// Battleground Flags
// *********************************************************************

bg_ctf,196,91,0	script	Alpha Crystal::Team1_Flag	1914,1,1,{
	end;

OnTouch:
	if( $@BG_Status != 1 || bg_current_mode() != bg_info("bg_ctf", BG_INFO_ID) || HP < 1 )
		end;

	if( getcharid(4) == $@BG_Team2 && .Flag_Status < 2 )
	{ // Flag Captured
		set .Flag_Status, 2; // Taken
		set .Flag_Carrier, getcharid(0);

		sc_end SC_HIDING;
		sc_end SC_CLOAKING;
		sc_end SC_CHASEWALK;
		// Renewal invisibility
		sc_end SC_CLOAKINGEXCEED;
		sc_end SC_CAMOUFLAGE;
		sc_end SC__INVISIBILITY;

		pcblock 1,1; // Block Skills/Items

		mapannounce "bg_ctf","Alpha Crystal Taken by [ " + strcharinfo(0) + " ]",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
		//bg_rankpoints "ctf_taken",1;
		disablenpc "Team1_Flag";
		addtimer 2000, "Flavius_CTF::OnAlphaFlash";
		stopnpctimer;
	}
	else if( getcharid(4) == $@BG_Team1 )
	{
		if( .Flag_Status == 0 && getvariableofnpc(.Flag_Carrier,"Team2_Flag") == getcharid(0) )
		{
			set getvariableofnpc(.Flag_Carrier,"Team2_Flag"),0;
			set .Flag_Carrier, 0;
			mapannounce "bg_ctf","Omega Crystal Captured by [ " + strcharinfo(0) + " ]!!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
			//bg_rankpoints "ctf_captured",1;
			pcblock 1,0; // UnBlock Skills/Items
			stopnpctimer;
			donpcevent "Flavius_CTF::OnTeam1Score";
		}
		else if( .Flag_Status == 1 )
		{
			mapannounce "bg_ctf","Alpha Crystal Returned by [ " + strcharinfo(0) + " ]!!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
			//bg_rankpoints "fame",1;
			disablenpc "Team1_Flag";
			sleep 2000;
			movenpc "Team1_Flag",196,91; // Back to Base
			set .Flag_Status, 0;
			initnpctimer;
			enablenpc "Team1_Flag";
		}
	}
	end;

OnTimer2000:
	stopnpctimer;
	if( .Flag_Status < 2 )
	{
		getmapxy .@m$, .@x, .@y, BL_NPC;
		viewpointmap "bg_ctf",1, .@x, .@y, 1, bg_info(bg_current_mode(), BG_INFO_COLOR);
		specialeffect 223;
		initnpctimer;
	}
	end;

OnBase:
	movenpc "Team1_Flag",196,91;
	set .Flag_Status, 0;
	set .Flag_Carrier, 0;
	initnpctimer;
	enablenpc "Team1_Flag";
	end;
}

bg_ctf,63,91,0	script	Omega Crystal::Team2_Flag	1915,1,1,{
	end;

OnTouch:
	if( $@BG_Status != 1 || bg_current_mode() != bg_info("bg_ctf", BG_INFO_ID) || HP < 1 )
		end;

	if( getcharid(4) == $@BG_Team1 && .Flag_Status < 2 )
	{ // Flag Captured
		set .Flag_Status, 2; // Taken
		set .Flag_Carrier, getcharid(0);

		sc_end SC_HIDING;
		sc_end SC_CLOAKING;
		sc_end SC_CHASEWALK;
		// Renewal invisibility
		sc_end SC_CLOAKINGEXCEED;
		sc_end SC_CAMOUFLAGE;
		sc_end SC__INVISIBILITY;

		pcblock 1,1; // Block Skills/Items

		mapannounce "bg_ctf","Omega Crystal Taken by [ " + strcharinfo(0) + " ]",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
		//bg_rankpoints "ctf_taken",1;
		disablenpc "Team2_Flag";
		addtimer 2000, "Flavius_CTF::OnOmegaFlash";
		stopnpctimer;
	}
	else if( getcharid(4) == $@BG_Team2 )
	{
		if( .Flag_Status == 0 && getvariableofnpc(.Flag_Carrier,"Team1_Flag") == getcharid(0) )
		{
			set getvariableofnpc(.Flag_Carrier,"Team1_Flag"),0;
			set .Flag_Carrier, 0;
			mapannounce "bg_ctf","Alpha Crystal Captured by [ " + strcharinfo(0) + " ]!!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
			//bg_rankpoints "ctf_captured",1;
			pcblock 1,0; // UnBlock Skills/Items
			stopnpctimer;
			donpcevent "Flavius_CTF::OnTeam2Score";
		}
		else if( .Flag_Status == 1 )
		{
			mapannounce "bg_ctf","Omega Crystal Returned by [ " + strcharinfo(0) + " ]!!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
			//bg_rankpoints "fame",1;
			disablenpc "Team2_Flag";
			sleep 2000;
			movenpc "Team2_Flag",63,91; // Back to Base
			set .Flag_Status, 0;
			initnpctimer;
			enablenpc "Team2_Flag";
		}
	}
	end;

OnTimer2000:
	stopnpctimer;
	if( .Flag_Status < 2 )
	{
		getmapxy .@m$, .@x, .@y, BL_NPC;
		viewpointmap "bg_ctf",1, .@x, .@y, 2, bg_info(bg_current_mode(), BG_INFO_COLOR);
		specialeffect 223;
		initnpctimer;
	}
	end;

OnBase:
	movenpc "Team2_Flag",63,91;
	set .Flag_Status, 0;
	set .Flag_Carrier, 0;
	initnpctimer;
	enablenpc "Team2_Flag";
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_CTF	-1,{
	end;

OnAlphaFlash:
	if( getvariableofnpc(.Flag_Carrier,"Team1_Flag") == getcharid(0) && $@BG_Status == 1 && bg_current_mode() == bg_info("bg_ctf", BG_INFO_ID) )
	{
		getmapxy .@m$, .@x, .@y, BL_PC;
		viewpointmap "bg_ctf",1, .@x, .@y, 1, bg_info(bg_current_mode(), BG_INFO_COLOR);
		specialeffect2 73;
		emotion ET_HELP,getcharid(3);
		addtimer 2000, "Flavius_CTF::OnAlphaFlash";
		percentheal -5,-5;
	}
	end;

OnOmegaFlash:
	if( getvariableofnpc(.Flag_Carrier,"Team2_Flag") == getcharid(0) && $@BG_Status == 1 && bg_current_mode() == bg_info("bg_ctf", BG_INFO_ID) )
	{
		getmapxy .@m$, .@x, .@y, BL_PC;
		viewpointmap "bg_ctf",1, .@x, .@y, 2, bg_info(bg_current_mode(), BG_INFO_COLOR);
		specialeffect2 73;
		emotion ET_HELP,getcharid(3);
		addtimer 2000, "Flavius_CTF::OnOmegaFlash";
		percentheal -5,-5;
	}
	end;

OnInit:
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";
	end;

OnTeam1Quit:
	bg_desert;
	set @killer_bg_src, 0;
	if( $@BG_Status != 0 && bg_current_mode() == bg_info("bg_ctf", BG_INFO_ID) )
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam1Die:
	if( $@BG_Status == 1 && bg_current_mode() == bg_info("bg_ctf", BG_INFO_ID) && getvariableofnpc(.Flag_Carrier,"Team2_Flag") == getcharid(0) )
	{ // Drop Flag
		set getvariableofnpc(.Flag_Carrier,"Team2_Flag"), 0;
		pcblock 1,0; // UnBlock Skills/Items
		getmapxy .@m$, .@x, .@y, BL_PC;
		movenpc "Team2_Flag", .@x, .@y;
		mapannounce "bg_ctf","Omega Flag Droped by [ " + strcharinfo(0) + " ]",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
		//bg_rankpoints "ctf_droped",1;
		//bg_rankpoints "fame",1,@killer_bg_src;
		set getvariableofnpc(.Flag_Status,"Team2_Flag"), 1; // OnFloor
		initnpctimer "Team2_Flag";
		enablenpc "Team2_Flag";
	}
	end;

OnTeam2Quit:
	bg_desert;
	set @killer_bg_src, 0;
	if( $@BG_Status != 0 && bg_current_mode() == bg_info("bg_ctf", BG_INFO_ID) )
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam2Die:
	if( $@BG_Status == 1 && bg_current_mode() == bg_info("bg_ctf", BG_INFO_ID) && getvariableofnpc(.Flag_Carrier,"Team1_Flag") == getcharid(0) )
	{ // Drop Flag
		set getvariableofnpc(.Flag_Carrier,"Team1_Flag"), 0;
		pcblock 1,0; // UnBlock Skills/Items
		getmapxy .@m$, .@x, .@y, BL_PC;
		movenpc "Team1_Flag", .@x, .@y;
		mapannounce "bg_ctf","Alpha Flag Droped by [ " + strcharinfo(0) + " ]",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
		//bg_rankpoints "ctf_droped",1;
		//bg_rankpoints "fame",1,@killer_bg_src;
		set getvariableofnpc(.Flag_Status,"Team1_Flag"), 1; // OnFloor
		initnpctimer "Team1_Flag";
		enablenpc "Team1_Flag";
	}
	end;
	
OnReady:
	if( bg_current_mode() != bg_info("bg_ctf", BG_INFO_ID) )
		end;
	set $@BG_Status, 1;
	initnpctimer;
	// BG Variables
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 2000;
	bg_warp $@BG_Team1,"bg_ctf",165,127;
	bg_warp $@BG_Team2,"bg_ctf",94,56;
	sleep 2000;
	// Respawn NPC's
	donpcevent "#guictf_respawn::OnBGStart";
	donpcevent "#croctf_respawn::OnBGStart";
	// Start Match!!
	donpcevent "Flavius_CTF::OnMatchStart";
	end;

OnBGTeam1Active:
	warp "bg_ctf",228,39;
	donpcevent "BG_Queue_Join::OnJoinEvent";
	end;

OnBGTeam2Active:
	warp "bg_ctf",30,38;
	donpcevent "BG_Queue_Join::OnJoinEvent";
	end;
	
OnMatchStart:
	if( $@BG_Status != 1 || bg_current_mode() != bg_info("bg_ctf", BG_INFO_ID) )
		end;

	// Flags2Base
	donpcevent "Team1_Flag::OnBase";
	donpcevent "Team2_Flag::OnBase";
	mapannounce "bg_ctf","The Flags have been set to their Bases!!",8;
	end;

OnTeam1Score:
	set .Team1_Score, .Team1_Score + 1;
	donpcevent "Flavius_CTF::OnMatchStop";
	end;

OnTeam2Score:
	set .Team2_Score, .Team2_Score + 1;
	donpcevent "Flavius_CTF::OnMatchStop";
	end;

OnMatchStop:
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";
	bg_updatescore "bg_ctf",.Team1_Score,.Team2_Score;

	viewpointmap "bg_ctf",2, 0, 0, 1, 0x0000FF;
	viewpointmap "bg_ctf",2, 0, 0, 2, 0xFF0000;

	if( .Team1_Score > 2 )
	{ // Team 1 Won
		mapannounce "bg_ctf","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius CTF!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
		donpcevent "Flavius_CTF::OnMatchEnd";
	}
	else if( .Team2_Score > 2 )
	{ // Team 2 Won
		mapannounce "bg_ctf","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius CTF!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
		donpcevent "Flavius_CTF::OnMatchEnd";
	}
	else
	{ // Keep Playing
		sleep 8000;
		donpcevent "Flavius_CTF::OnMatchStart";
	}
	end;

OnTimer600000:
	mapannounce "bg_ctf","The Battle will ends in 5 minutes!!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
	end;

OnTimer840000:
	mapannounce "bg_ctf","The Battle will ends in 1 minute!!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
	end;

OnTimer900000:
OnPrematureEnd:
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";

	viewpointmap "bg_ctf",2, 0, 0, 1, bg_info(bg_current_mode(), BG_INFO_COLOR);
	viewpointmap "bg_ctf",2, 0, 0, 2, bg_info(bg_current_mode(), BG_INFO_COLOR);

	if( .Team1_Score > .Team2_Score )
		mapannounce "bg_ctf","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius CTF!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
	else if( .Team1_Score < .Team2_Score )
		mapannounce "bg_ctf","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius CTF!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
	else
		mapannounce "bg_ctf","The battle is over. This is a Tie...!",1,bg_info(bg_current_mode(), BG_INFO_COLOR);
	donpcevent "Flavius_CTF::OnMatchEnd";
	end;

OnMatchEnd:
	stopnpctimer;
	disablenpc "Team1_Flag";
	disablenpc "Team2_Flag";
	donpcevent "#guictf_respawn::OnBGStop";
	donpcevent "#croctf_respawn::OnBGStop";
	set $@BG_Status, 2;
	bg_reserve "bg_ctf", true;
	// =======================================================
	// Team Rewards
	// =======================================================
	if( .Team1_Score > .Team2_Score )
	{
		callfunc("Reward_BG","bg_ctf", 1);
	}
	else if( .Team2_Score > .Team1_Score )
	{
		callfunc("Reward_BG","bg_ctf", 2);
	}
	else
	{
		callfunc("Reward_BG","bg_ctf", 0);
	}
	// =======================================================
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 5000;
	bg_unbook "bg_ctf";
	donpcevent "BG_Queue_Join::OnRotate";
	bg_updatescore "bg_ctf",0,0;
	end;
}

// Battleground Therapist
// *********************************************************************
bg_ctf,228,45,5	script	Therapist in battle#ctf1	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, take a deep breath";
	mes "and you free yourself from the pain.";
	specialeffect2 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

bg_ctf,31,45,5	script	Therapist in battle#ctf2	4_F_SISTER,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, take a deep breath";
	mes "and you free yourself from the pain.";
	specialeffect2 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}
// Battleground Respawn
// *********************************************************************

bg_ctf,228,39,0	script	#guictf_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bg_ctf",214,51,244,22,100,100;
	areawarp "bg_ctf",214,51,244,22,"bg_ctf",191,121;
	initnpctimer;
	end;
}

bg_ctf,30,38,0	script	#croctf_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bg_ctf",16,51,45,24,100,100;
	areawarp "bg_ctf",16,51,45,24,"bg_ctf",68,62;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bg_ctf,166,86,1	duplicate(Base Flag#bg)	Alpha Base#bat23	973
bg_ctf,166,97,1	duplicate(Base Flag#bg)	Alpha Base#bat24	973

bg_ctf,93,86,1	duplicate(Base Flag#bg)	Omega Base#bat22	974
bg_ctf,93,97,1	duplicate(Base Flag#bg)	Omega Base#bat23	974

// MapFlags
// *********************************************************************

bg_ctf	mapflag	battleground	2
bg_ctf	mapflag	nomemo
bg_ctf	mapflag	nosave	SavePoint
bg_ctf	mapflag	noteleport
bg_ctf	mapflag	nowarp
bg_ctf	mapflag	nowarpto
bg_ctf	mapflag	noreturn
bg_ctf	mapflag	nobranch
bg_ctf	mapflag	nopenalty
//bg_ctf	mapflag	noemergencycall
bg_ctf	mapflag	nodrop
